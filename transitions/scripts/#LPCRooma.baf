///////////////////////////////////////////////////////////////
// Updates needed for the house in front of the Ducal Palace //
// Patched via eltan_house0.tpa                                     //
///////////////////////////////////////////////////////////////

IF
	AreaCheck("#LBG0006")	// House in front of the palace
	Global("#L_HousePrepped","MYAREA",0)
THEN
	RESPONSE #100
		MoveContainerContents("%NBaldursGate_RinniesHouse_L1%*Container1","#LBG0006*PlayerChest00")
		MoveContainerContents("%NBaldursGate_RinniesHouse_L1%*Container2","#LBG0006*Container2")
		MoveContainerContents("%NBaldursGate_RinniesHouse_L1%*Container3","#LBG0006*Container3")
		SetGlobal("#L_HousePrepped","MYAREA",1)
		Continue()
END

IF
	AreaCheck("#LBG0006")	// House in front of the palace
	!Global("#L_PlayerChestLocation","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("#L_PlayerChestLocation","GLOBAL",2)
		MoveContainerContents("#LBD0103*PlayerChest00","#LBG0006*PlayerChest00") // #1 BG1 Room in Palace
		%SOD_CHEST_TRANS%
		SmallWait(1)
		Continue()
END

IF
	AreaCheck("#LBG0006")	// House in front of the palace
	Global("#L_FromBG0200","GLOBAL",1)	// SoD area 1
	Global("#L_SetForBG","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_SetForBG","MYAREA",1)
		SetGlobal("#L_SetForSoD1","MYAREA",0)
		SetGlobal("#L_SetForSoD2","MYAREA",0)
		TriggerActivation("Door0200",TRUE)
		TriggerActivation("DoorSoD1",FALSE)
		TriggerActivation("DoorSoD2",FALSE)
		Continue()
END

IF
	AreaCheck("#LBG0006")	// House in front of the palace
	Global("#L_FromBD0010","GLOBAL",1)	// SoD area 1
	Global("#L_SetForSoD1","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_SetForBG","MYAREA",0)
		SetGlobal("#L_SetForSoD1","MYAREA",1)
		SetGlobal("#L_SetForSoD2","MYAREA",0)
		TriggerActivation("Door0200",FALSE)
		TriggerActivation("DoorSoD1",TRUE)
		TriggerActivation("DoorSoD2",FALSE)
		Continue()
END

IF
	AreaCheck("#LBG0006")	// House in front of the palace
	Global("#L_FromBD0101","GLOBAL",1)	// SoD area 2
	Global("#L_SetForSoD2","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_SetForBG","MYAREA",0)
		SetGlobal("#L_SetForSoD1","MYAREA",0)
		SetGlobal("#L_SetForSoD2","MYAREA",1)
		TriggerActivation("Door0200",FALSE)
		TriggerActivation("DoorSoD1",FALSE)
		TriggerActivation("DoorSoD2",TRUE)
		Continue()
END

