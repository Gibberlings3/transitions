/////////////////////////////////////////
// Check for various timer expirations //
// Compiled via timers_common.tpa      //
/////////////////////////////////////////

// Refugee wave triggers and counters
IF
	GlobalGT("#L_NextWaveTimer","GLOBAL",0)
	GlobalTimerExpired("#L_NextWaveTimer","GLOBAL")
	GlobalLT("#L_LastWaveSet","GLOBAL",3) // We're only doing 3 waves for the time being
	GlobalLT("#L_WavesToSet","GLOBAL",3)
THEN
	RESPONSE #100
		IncrementGlobal("#L_WavesToSet","GLOBAL",1)
		SetGlobalTimer("#L_NextWaveTimer","GLOBAL",SEVEN_DAYS)
		Continue()
END

// Timer to see if group needs to transfer to SoD/BG2 but it won't happen via the dialogue as normal
IF
    !InPartyAllowDead("IMOEN2")
	Global("#L_ExitTimer","GLOBAL",0)		// the exit dialogue never fired
	Global("#L_SoloExitLeft","GLOBAL",0)
	!Global("#L_SoloExitTimer","GLOBAL",0)
	GlobalTimerExpired("#L_SoloExitTimer","GLOBAL")
	Global("#L_SarvQuests","GLOBAL",100)
	GlobalGT("#L_StartCaelarAttack","GLOBAL",0)
	GlobalLT("#L_StartCaelarAttack","GLOBAL",99)
	Global("EndOfBG1","GLOBAL",1)
	OR(3)
        !InParty(Player2)
        NextTriggerObject(Player2)
        !Global("#L_SoDExitModded","LOCALS",1) // Let NPCs modded to leave go first
    OR(3)
        !InParty(Player3)
        NextTriggerObject(Player3)
        !Global("#L_SoDExitModded","LOCALS",1) // Let NPCs modded to leave go first
    OR(3)
        !InParty(Player4)
        NextTriggerObject(Player4)
        !Global("#L_SoDExitModded","LOCALS",1) // Let NPCs modded to leave go first
    OR(3)
        !InParty(Player5)
        NextTriggerObject(Player5)
        !Global("#L_SoDExitModded","LOCALS",1) // Let NPCs modded to leave go first
    OR(3)
        !InParty(Player6)
        NextTriggerObject(Player6)
        !Global("#L_SoDExitModded","LOCALS",1) // Let NPCs modded to leave go first
 THEN
	RESPONSE #100
		ClearAllActions() 
		SetGlobal("#L_SoloExitLeft","GLOBAL",1) 
		SetWorldmap("BGMap")
		SetCutSceneLite(TRUE)
		StartCutSceneEx("#L_Cut07",TRUE)
END

IF
	Global("#L_ExitTimer","GLOBAL",0)		// the exit dialogue never fired
	!Global("#L_SoloExitTimer","GLOBAL",0)
	GlobalTimerExpired("#L_SoloExitTimer","GLOBAL")
	Global("#L_SarvQuests","GLOBAL",100)
	Global("#L_StartBG2","GLOBAL",1)
	Global("EndOfBG1","GLOBAL",2)
	OR(3)
    	!InParty(Player2)
		NextTriggerObject(Player2)
      	!Global("#L_BG2ExitModded","LOCALS",1) // Let NPCs modded to leave go first
   	OR(3)
      	!InParty(Player3)
      	NextTriggerObject(Player3)
      	!Global("#L_BG2ExitModded","LOCALS",1) // Let NPCs modded to leave go first
   	OR(3)
      	!InParty(Player4)
      	NextTriggerObject(Player4)
      	!Global("#L_BG2ExitModded","LOCALS",1) // Let NPCs modded to leave go first
   	OR(3)
      	!InParty(Player5)
      	NextTriggerObject(Player5)
      	!Global("#L_BG2ExitModded","LOCALS",1) // Let NPCs modded to leave go first
   	OR(3)
      	!InParty(Player6)
      	NextTriggerObject(Player6)
      	!Global("#L_BG2ExitModded","LOCALS",1) // Let NPCs modded to leave go first
THEN
	RESPONSE #100
		ClearAllActions()
		SetGlobal("#L_StartBG2","GLOBAL",2)
		StartCutSceneMode()
		StartCutSceneEx("#L_Cut09",TRUE)
END

