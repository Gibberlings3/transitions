////////////////////////////////////////////////////////////
// Updates the contents of PC's chest in the pocket plane //
// Patched via main_eet.tpa                               //
//////////////////////////////////////////////////////////

// Activate the chest when the timer has expired
IF
	Global("#L_PCChest","MYAREA",1) // PC requested it to be found
	GlobalTimerExpired("#L_PCChestTimer","MYAREA")
THEN
	RESPONSE #100
		SetGlobal("#L_PCChest","MYAREA",2)
		MoveContainerContents("AR4500*Container1","AR4500*#LPlayerChest")
		ContainerEnable("#LPlayerChest",TRUE)
		ContainerEnable("Container 1",FALSE)
    	Continue()
END

// Move everything from the PC's chest whereever it is at the moment
// to their chest whereever they are located now
// Will need to update this for every possible house award
// OMG!  SAPIENT PEARWOOD!
// Or busy servants ... who knows.
// Transfer doesn't happen during SoD but afterwards the SoD chest needs to 'come home'
IF
	GlobalGT("#L_PCChest","MYAREA",1)
	!Global("#L_PlayerChestLocation","GLOBAL",30)
	Global("#L_MoveContents","MYAREA",0)
	TriggerOverride("#LPlayerChest",Range([PC],30))
THEN
	RESPONSE #100
		SetGlobal("#L_MoveContents","MYAREA",1)
		Continue()
END

IF
	Global("#L_MoveContents","MYAREA",1)
	Global("#L_HouseBGate1","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("#LBG0006*PlayerChest00","AR4500*#LPlayerChest")
		SmallWait(1)
		Continue()
END

IF
	Global("#L_MoveContents","MYAREA",1)
	Global("#L_HouseBGate2","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("#LBG1107*PlayerChest00","AR4500*#LPlayerChest")
		SmallWait(1)
		Continue()
END

IF
	Global("#L_MoveContents","MYAREA",1)
	Global("#L_HouseHHedge","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("#LBG3201*PlayerChest00","AR4500*#LPlayerChest")
		SmallWait(1)
		Continue()
END

IF
	Global("#L_MoveContents","MYAREA",1)
	Global("#L_HouseBeregost","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("#LBG3347*PlayerChest00","AR4500*#LPlayerChest")
		SmallWait(1)
		Continue()
END

IF
	Global("#L_MoveContents","MYAREA",1)
THEN
	RESPONSE #100
		SetGlobal("#L_MoveContents","MYAREA",0)
		SetGlobal("#L_PlayerChestLocation","GLOBAL",30)
		MoveContainerContents("#LBD0103*PlayerChest00","AR4500*#LPlayerChest")	// BG1 Room in Palace
		MoveContainerContents("BD0103*PlayerChest00","AR4500*#LPlayerChest") 	// SoD Room in Palace
		MoveContainerContents("BD1000*PlayerChest00","AR4500*#LPlayerChest") 	// 1st SoD Camp
		MoveContainerContents("BD7100*PlayerChest00","AR4500*#LPlayerChest") 	// 2nd SoD Camp
		MoveContainerContents("BD3000*PlayerChest00","AR4500*#LPlayerChest")	// 3rd SoD Camp
		MoveContainerContents("AR0523*container4","AR4500*#LPlayerChest")		// Bard Stronghold
		MoveContainerContents("AR1306*container11","AR4500*#LPlayerChest")		// Fighter Stronghold
		MoveContainerContents("AR0322*container6","AR4500*#LPlayerChest")		// Thief Stronghold
		MoveContainerContents("AR1107*#LPlayerChest","AR4500*#LPlayerChest")	// Ranger Stronghold
		MoveContainerContents("AR0411*#LPlayerChest","AR4500*#LPlayerChest")	// Mage Stronghold
		MoveContainerContents("AR1902*#LPlayerChest","AR4500*#LPlayerChest")	// Druid Stronghold
		MoveContainerContents("AR0901*#LPlayerChest","AR4500*#LPlayerChest")	// Neutral Cleric Stronghold
		MoveContainerContents("AR0902*#LPlayerChest","AR4500*#LPlayerChest")	// Good Cleric Stronghold
		MoveContainerContents("AR0903*#LPlayerChest","AR4500*#LPlayerChest")	// Paladin Stronghold
		MoveContainerContents("AR0904*#LPlayerChest","AR4500*#LPlayerChest")	// Evil Cleric Stronghold
		SmallWait(1)
		Continue()
END

// Have the imp inform the PC
IF
	Exists("botsmith")
	Global("#L_PCChest","MYAREA",2)
	TriggerOverride("botsmith",Range(Player1,15))
THEN
	RESPONSE #100
		ActionOverride("botsmith",StartDialogueNoSet(Player1))
END

