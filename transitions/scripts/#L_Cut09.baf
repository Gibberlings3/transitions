///////////////////////////////////////////////////////////////////////
// Removes all NPCs from the group                                   //
// Sends the rest of the group (player characters) and canon group   //
//   to area outside of Baldur's Gate for pre-BG2 capture scene      //
// Parsed via main_eet.tpa                                           //
// Called via solo exit timer in solo_check.baf                      //
///////////////////////////////////////////////////////////////////////

IF
	Global("#L_CanonGroupMoved","GLOBAL",0)
	InPartyAllowDead(Player6)
	!Name("None",Player6)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride(Player6,LeaveParty())
		Continue()
END

IF
	Global("#L_CanonGroupMoved","GLOBAL",0)
	InPartyAllowDead(Player5)
	!Name("None",Player5)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride(Player5,LeaveParty())
		Continue()
END

IF
	Global("#L_CanonGroupMoved","GLOBAL",0)
	InPartyAllowDead(Player4)
	!Name("None",Player4)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride(Player4,LeaveParty())
		Continue()
END

IF
	Global("#L_CanonGroupMoved","GLOBAL",0)
	InPartyAllowDead(Player3)
	!Name("None",Player3)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride(Player3,LeaveParty())
		Continue()
END

IF
	Global("#L_CanonGroupMoved","GLOBAL",0)
	InPartyAllowDead(Player2)
	!Name("None",Player2)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride(Player2,LeaveParty())
		Continue()
END

IF
	Global("#L_CanonGroupMoved","GLOBAL",0)
THEN
	RESPONSE #100
		CutSceneId(Player1)

		// Set exit globals
		SetGlobal("#L_SarvQuests","GLOBAL",100)
		SetGlobal("EndOfBG1","GLOBAL",1)
		SetGlobal("bd_player_exiled","GLOBAL",2) // To indicate that we had nothing to do with it (since we're cutting out before it happened)

		SetGlobal("#L_CanonGroupMoved","GLOBAL",1)

		SmallWait(10)

		DisableAI(Player1,TRUE)
		DisableAI(Player2,TRUE)
		DisableAI(Player3,TRUE)
		DisableAI(Player4,TRUE)
		DisableAI(Player5,TRUE)
		DisableAI(Player6,TRUE)

		MoveGlobal("BD6100","%IMOEN_DV%",[515.320])  // Imoen
		ReallyForceSpellDeadRES("bdrejuve","%IMOEN_DV%")  // No such index
		ActionOverride("%IMOEN_DV%",ChangeAIScript("BDIMOEN",OVERRIDE))
		ActionOverride("%IMOEN_DV%",ChangeAIScript("",CLASS))
		ActionOverride("%IMOEN_DV%",ChangeAIScript("",RACE))
		ActionOverride("%IMOEN_DV%",ChangeAIScript("",GENERAL))
		ActionOverride("%IMOEN_DV%",ChangeAIScript("",DEFAULT))
		ActionOverride("%IMOEN_DV%",SetDialogue("BDIMOEN"))
		DisableAI("%IMOEN_DV%",TRUE)

		ChangeEnemyAlly("%IMOEN_DV%",NEUTRAL)
		ChangeSpecifics("%IMOEN_DV%",ALLIES)

		MoveGlobal("bd6100","MINSC",[545.260])  // Minsc
		ReallyForceSpellDeadRES("bdrejuve","minsc")  // No such index
		ActionOverride("minsc",ChangeAIScript("bdminsc",OVERRIDE))
		ActionOverride("minsc",ChangeAIScript("",CLASS))
		ActionOverride("minsc",ChangeAIScript("SHOUT",RACE))
		ActionOverride("minsc",ChangeAIScript("BDFIGH01",GENERAL))
		ActionOverride("minsc",ChangeAIScript("",DEFAULT))
		ActionOverride("minsc",SetDialogue("bdminsc"))
		DisableAI("MINSC",TRUE)

		ChangeEnemyAlly("MINSC",NEUTRAL)
		ChangeSpecifics("MINSC",ALLIES)

		MoveGlobal("bd6100","DYNAHEIR",[485.280])  // Dynaheir
		ReallyForceSpellRES("bdrejuve","dynaheir")  // No such index
		ActionOverride("dynaheir",ChangeAIScript("bddynahe",OVERRIDE))
		ActionOverride("dynaheir",ChangeAIScript("",CLASS))
		ActionOverride("dynaheir",ChangeAIScript("SHOUT",RACE))
		ActionOverride("dynaheir",ChangeAIScript("BDMAGE01",GENERAL))
		ActionOverride("dynaheir",ChangeAIScript("",DEFAULT))
		ActionOverride("dynaheir",SetDialogue("bddynahe"))
		DisableAI("DYNAHEIR",TRUE)

		ChangeEnemyAlly("DYNAHEIR",NEUTRAL)
		ChangeSpecifics("DYNAHEIR",ALLIES)

		MoveGlobal("bd6100","KHALID",[515.210])  // Khalid
		ReallyForceSpellDeadRES("bdrejuve","khalid")  // No such index
		ActionOverride("khalid",ChangeAIScript("bdkhalid",OVERRIDE))
		ActionOverride("khalid",ChangeAIScript("",CLASS))
		ActionOverride("khalid",ChangeAIScript("SHOUT",RACE))
		ActionOverride("khalid",ChangeAIScript("BDFIGH01",GENERAL))
		ActionOverride("khalid",ChangeAIScript("",DEFAULT))
		ActionOverride("khalid",SetDialogue("bdkhalid"))
		DisableAI("KHALID",TRUE)

		ChangeEnemyAlly("KHALID",NEUTRAL)  // Imoen
		ChangeSpecifics("KHALID",ALLIES)  // Imoen

		MoveGlobal("bd6100","JAHEIRA",[450.250])  // Jaheira
		ReallyForceSpellDeadRES("bdrejuve","jaheira")  // No such index
		ActionOverride("jaheira",ChangeAIScript("bdjaheir",OVERRIDE))
		ActionOverride("jaheira",ChangeAIScript("",CLASS))
		ActionOverride("jaheira",ChangeAIScript("SHOUT",RACE))
		ActionOverride("jaheira",ChangeAIScript("bddrui01",GENERAL))
		ActionOverride("jaheira",ChangeAIScript("",DEFAULT))
		ActionOverride("jaheira",SetDialogue("bdjaheir"))
		DisableAI("JAHEIRA",TRUE)

		ChangeEnemyAlly("JAHEIRA",NEUTRAL)  // Imoen
		ChangeSpecifics("JAHEIRA",ALLIES)  // Imoen

		SmallWait(10)

		// If anyone is left in the group, they'll be player characters
		ActionOverride(Player2,LeaveAreaLUA("bd6100","",[535.340],SE))
		ActionOverride(Player3,LeaveAreaLUA("bd6100","",[565.280],SE))
		ActionOverride(Player4,LeaveAreaLUA("bd6100","",[505.300],SE))
		ActionOverride(Player5,LeaveAreaLUA("bd6100","",[535.230],SE))
		ActionOverride(Player6,LeaveAreaLUA("bd6100","",[600.330],SE))
		LeaveAreaLUA("BD6100","",[470.270],SE)
		MultiPlayerSync()		
		
		StartCutSceneEx("#L_Add01",TRUE)
END

