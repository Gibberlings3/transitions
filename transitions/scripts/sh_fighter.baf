/////////////////////////////////////////////////////////////
// Spawn servant in the protagonist's stronghold (fighter) //
// Updates the contents of PC's chest                      //
// Patched via main_eet.tpa                                //
/////////////////////////////////////////////////////////////

// Spawn the servant the first time the PC enters their stronghold
IF
	Global("#L_ServantSpawned","MYAREA",0)
	GlobalGT("bd_player_exiled","GLOBAL",0)		// Must be on friendly terms with the dukes
	OR(5)										// Must have a chest somewhere in BG1/SoD
		Global("#L_AcceptedRoom","GLOBAL",1)
		Global("#L_HouseBGate1","GLOBAL",2)
		Global("#L_HouseBGate2","GLOBAL",2)
		Global("#L_HouseBeregost","GLOBAL",2)
		Global("#L_HouseHHedge","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("#L_ServantSpawned","MYAREA",1)
    	CreateCreature("#LDPSrv1",[1500.1335],S)
    	Continue()
END

// Have the servant welcome the PC
IF
	Exists("#LDPSrv1")
	TriggerOverride("#LDPSrv1",Range(Player1,15))
THEN
	RESPONSE #100
		ActionOverride("#LDPSrv1",StartDialogueNoSet(Player1))
END

// Move everything from the PC's chest whereever it is at the moment
// to their chest whereever they are located now
// Will need to update this for every possible house award
// OMG!  SAPIENT PEARWOOD!
// Or busy servants ... who knows.
// Transfer doesn't happen during SoD but afterwards the SoD chest needs to 'come home'
IF
	Global("#L_ServantSpawned","MYAREA",2)
	!Global("#L_PlayerChestLocation","GLOBAL",22)
	Global("#L_HouseBGate1","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("#LBG0006*PlayerChest00","AR1306*container11")
		SmallWait(1)
		Continue()
END

IF
	Global("#L_ServantSpawned","MYAREA",2)
	!Global("#L_PlayerChestLocation","GLOBAL",22)
	Global("#L_HouseBGate2","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("#LBG1107*PlayerChest00","AR1306*container11")
		SmallWait(1)
		Continue()
END

IF
	Global("#L_ServantSpawned","MYAREA",2)
	!Global("#L_PlayerChestLocation","GLOBAL",22)
	Global("#L_HouseHHedge","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("#LBG3201*PlayerChest00","AR1306*container11")
		SmallWait(1)
		Continue()
END

IF
	Global("#L_ServantSpawned","MYAREA",2)
	!Global("#L_PlayerChestLocation","GLOBAL",22)
	Global("#L_HouseBeregost","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("#LBG3347*PlayerChest00","AR1306*container11")
		SmallWait(1)
		Continue()
END

IF
	Global("#L_ServantSpawned","MYAREA",2)
	!Global("#L_PlayerChestLocation","GLOBAL",22)
THEN
	RESPONSE #100
		SetGlobal("#L_PlayerChestLocation","GLOBAL",22)
		MoveContainerContents("#LBD0103*PlayerChest00","AR1306*container11")	// BG1 Room in Palace
		MoveContainerContents("BD0103*PlayerChest00","AR1306*container11") 		// SoD Room in Palace
		MoveContainerContents("BD1000*PlayerChest00","AR1306*container11") 		// 1st SoD Camp
		MoveContainerContents("BD7100*PlayerChest00","AR1306*container11") 		// 2nd SoD Camp
		MoveContainerContents("BD3000*PlayerChest00","AR1306*container11")		// 3rd SoD Camp
		MoveContainerContents("AR0523*container4","AR1306*container11")			// Bard Stronghold
		MoveContainerContents("AR0322*container5","AR1306*container11")			// Thief Stronghold
		MoveContainerContents("AR1107*#LPlayerChest","AR1306*container11")		// Ranger Stronghold
		MoveContainerContents("AR0411*#LPlayerChest","AR1306*container11")		// Mage Stronghold
		MoveContainerContents("AR1902*#LPlayerChest","AR1306*container11")		// Druid Stronghold
		MoveContainerContents("AR0901*#LPlayerChest","AR1306*container11")		// Neutral Cleric Stronghold
		MoveContainerContents("AR0902*#LPlayerChest","AR1306*container11")		// Good Cleric Stronghold
		MoveContainerContents("AR0903*#LPlayerChest","AR1306*container11")		// Paladin Stronghold
		MoveContainerContents("AR0904*#LPlayerChest","AR1306*container11")		// Evil Cleric Stronghold
		MoveContainerContents("AR4500*#LPlayerChest","AR1306*container11")		// Pocket Plane
		SmallWait(1)
		Continue()
END

