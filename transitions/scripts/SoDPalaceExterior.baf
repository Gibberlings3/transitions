///////////////////////////////////////////////////
// Let's PC's house know where they entered from //
// Parsed via eltan_house0.tpa                   //
///////////////////////////////////////////////////

IF
	Global("#L_HouseBGate1","GLOBAL",2)
	Global("#L_HousePrepped","MYAREA",0)
THEN
	RESPONSE #100
		AddMapNote([60.409],@1013)
		SetGlobal("#L_HousePrepped","MYAREA",1)
		Continue()
END

IF
	Global("#L_DoorTimerSet","MYAREA",0)
	OpenState("Port0006",TRUE)
THEN
	RESPONSE #100
		// Keeping the door open for 10 seconds should be more than enough time 
		RealSetGlobalTimer("#L_DoorTimer","MYAREA",10)
		SetGlobal("#L_DoorTimerSet","MYAREA",1)
		Continue()
END

IF
	Global("#L_DoorTimerSet","MYAREA",1)
	RealGlobalTimerExpired("#L_DoorTimer","MYAREA")
THEN
	RESPONSE #100
		CloseDoor("Port0006")
		SmallWait(1)
		Lock("Port0006")
		SmallWait(1)
		SetGlobal("#L_DoorTimerSet","MYAREA",0)
		Continue()
END

IF
	AreaCheck("BD0010")
THEN
	RESPONSE #100
		SetGlobal("#L_FromBG0200","GLOBAL",0)
		SetGlobal("#L_FromBD0010","GLOBAL",1)
		SetGlobal("#L_FromBD0101","GLOBAL",0)
END

IF
	AreaCheck("BD0101")
THEN
	RESPONSE #100
		SetGlobal("#L_FromBG0200","GLOBAL",0)
		SetGlobal("#L_FromBD0010","GLOBAL",0)
		SetGlobal("#L_FromBD0101","GLOBAL",1)
END
