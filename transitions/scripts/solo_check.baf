//////////////////////////////////////////////////////
// Timer to see if it's time to transfer to SoD/BG2 //
// Compiled via main_common.tpa                     //
//////////////////////////////////////////////////////


IF
  OR(4)
    Global("#L_StartBG2","GLOBAL",1)            // We're starting either SoD or BG2
    Global("#L_StartCaelarAttack","GLOBAL",1)
    Global("#L_StartCaelarAttack","GLOBAL",2)
    Global("#L_StartCaelarAttack","GLOBAL",3)
  OR(6)
    TriggerOverride(Player2,Global("#L_SoDExitModded","LOCALS",1))  // Someone in the group needs us to wait
    TriggerOverride(Player3,Global("#L_SoDExitModded","LOCALS",1))
    TriggerOverride(Player4,Global("#L_SoDExitModded","LOCALS",1))
    TriggerOverride(Player5,Global("#L_SoDExitModded","LOCALS",1))
    TriggerOverride(Player6,Global("#L_SoDExitModded","LOCALS",1))
    TriggerOverride(Player1,False())
THEN
  RESPONSE #100
    SetGlobalTimer("#L_SoloExitTimer","GLOBAL",ONE_ROUND)         // Bump the timer
END

IF
	Global("#L_SoloExitLeft","GLOBAL",0)
	!Global("#L_SoloExitTimer","GLOBAL",0)
	GlobalTimerExpired("#L_SoloExitTimer","GLOBAL")
	Global("#L_SarvQuests","GLOBAL",100)
	GlobalGT("#L_StartCaelarAttack","GLOBAL",0)
	GlobalLT("#L_StartCaelarAttack","GLOBAL",99)
	Global("EndOfBG1","GLOBAL",1)
  Global("#L_WaitForMe","GLOBAL",0)
THEN
	RESPONSE #100
		ClearAllActions() 
		SetGlobal("#L_SoloExitLeft","GLOBAL",1) 
		SetWorldmap("BGMap")
		SetCutSceneLite(TRUE)
		StartCutSceneEx("#L_Cut07",TRUE)
END

IF
  !Global("#L_SoloExitTimer","GLOBAL",0)
  GlobalTimerExpired("#L_SoloExitTimer","GLOBAL")
  Global("#L_SarvQuests","GLOBAL",100)
  Global("#L_StartBG2","GLOBAL",1)
  Global("EndOfBG1","GLOBAL",2)
  Global("#L_WaitForMe","GLOBAL",0)
THEN
  RESPONSE #100
    ClearAllActions()
    SetGlobal("#L_StartBG2","GLOBAL",2)
    StartCutSceneMode()
    StartCutSceneEx("#L_Cut09",TRUE)
END

