/////////////////////////////////////////////////////////////////////////
// Main component that allows continued play after Sarevok is defeated //
// This options uses the BG1 end movie as part of a dream sequence     //
// that gives the protagonist an additional bhaal-spawn power          //
/////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION main0 BEGIN
   // New dream and bhaal-spawn power
   COPY_EXISTING ~PLAYER1D.BCS~ ~override~
      DECOMPILE_AND_PATCH BEGIN 
         APPEND_FILE TEXT ~%mod_root%/scripts/newdrm1.baf~
      END
   BUT_ONLY_IF_IT_CHANGES

   // See if jastey's Endless BG1 mod has been installed
   ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1/c#endlessbg1.tp2~ 0) BEGIN
      // Replace jastey's play end-movie block with block that 
      // just opens the door and sets global variables
      LAF REPLACE_SCRIPT_BLOCK
         INT_VAR
            only_once = 1
         STR_VAR 
            script = BG0125
            match = ~Global("C#st_BG1End","GLOBAL",0)~
            insert = ~%mod_root%/scripts/BG0125a.baf~
      END
   END ELSE BEGIN
      // Replace original block that transitions to SoD with block that 
      // just opens the door and sets global variables
      LAF REPLACE_SCRIPT_BLOCK
         INT_VAR
            only_once = 1
         STR_VAR 
            script = BG0125
            match = ~StartCutSceneEx("BDSODTRN",TRUE)~
            insert = ~%mod_root%/scripts/BG0125a.baf~
      END
   END
   COPY_EXISTING ~CUT41I.BCS~ ~override~
      DECOMPILE_AND_PATCH BEGIN
         REPLACE_TEXTUALLY ~ReallyForceSpell(Player1,LOSE_ALL)~ ~~~~~ReallyForceSpell(Player1,LOSE_ALL) RemoveSpell(~SPIN200~) RemoveSpell(~SPIN202~)~~~~~
      END
   BUT_ONLY_IF_IT_CHANGES

   // Do actions that are needed no matter which ending was chosen
   INCLUDE ~%mod_root%/components/main_common.tpa~
END