/////////////////////////////////////////////////////////
// Actions common to all options of the main component //
// For BG1 and EET                                     //
// Included via main_common.tpa                        //
/////////////////////////////////////////////////////////

ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ 0) BEGIN
	// Remove jastey's code to prep the dukes in the palace - we do it differently
	LAF DELETE_SCRIPT_BLOCK
		INT_VAR
			only_once = 0
		STR_VAR
			script = EVAL ~%NBaldursGate_DucalPalace_L1%~
			match = ~C#st_RearrangeDukes~
	END
	LAF DELETE_SCRIPT_BLOCK
		INT_VAR
			only_once = 0
		STR_VAR
			script = EVAL ~%NBaldursGate_DucalPalace_L1%~
			match = ~C#st_SpawnLiia~
	END
	EXTEND_TOP ~%Undercity_TempleofBhaal%.BCS~ ~%mod_root%/scripts/bg0125_liia.baf~

	// Remove jastey's guards and opening of the palace
	LAF DELETE_SCRIPT_BLOCK
		INT_VAR
			only_once = 0
		STR_VAR
			script = EVAL ~%NBaldursGate%~
			match = ~C#st_SarevokDeadGuards~
	END
	LAF DELETE_SCRIPT_BLOCK
		INT_VAR
			only_once = 0
		STR_VAR
			script = EVAL ~%NBaldursGate%~
			match = ~C#st_SarevokDeadOpenGate~
	END
END

// Replace the block that transfers to SoD or plays the movie so play can continue
ACTION_IF (GAME_INCLUDES ~sod~) BEGIN
	LAF REPLACE_SCRIPT_BLOCK
		INT_VAR
			only_once = 1
		STR_VAR 
			script = EVAL ~%Undercity_TempleofBhaal%~
			match = ~StartCutSceneEx("BDSODTRN",TRUE)~
			insert = ~%mod_root%/scripts/BG0125_main.baf~
	END 
END ELSE BEGIN
	LAF REPLACE_SCRIPT_BLOCK
		INT_VAR
			only_once = 1
		STR_VAR 
			script = EVAL ~%Undercity_TempleofBhaal%~
			match = ~StartMovie("ENDMOVIE")~
			insert = ~%mod_root%/scripts/BG0125_main.baf~
	END
END

// Force chapter dreams at first rest so we're not ending BG1 and getting Chapter 6's dream
COPY ~%mod_root%/objects/#LDRM2.2da~ ~override~
COPY ~%mod_root%/objects/#LDRM3.2da~ ~override~
COPY ~%mod_root%/objects/#LDRM4.2da~ ~override~
COPY ~%mod_root%/objects/#LDRM5.2da~ ~override~
COPY ~%mod_root%/objects/#LDRM6.2da~ ~override~
COPY ~%mod_root%/objects/#LDRM7.2da~ ~override~
EXTEND_TOP ~BALDUR.BCS~ ~%mod_root%/scripts/setnextdream.baf~
EXTEND_BOTTOM ~PLAYER1D.BCS~ ~%mod_root%/scripts/forcedrms.baf~

// Put in all the quest journal entries here so they're all in as a group no matter which quests are available
ADD_JOURNAL @3002 @3003 @3004 @3005 @3006 @3007 @3008 @3009 @3010 USING ~%tra_loc%/%s/dialogues.tra~	// Clean-up quests
ADD_JOURNAL @3011 @3012 @3013 @3014 USING ~%tra_loc%/%s/dialogues.tra~									// Dead three research quest

////////////////////////////////////////////////////////////
// Now allowing this to happen in BG1 with or without SoD //
////////////////////////////////////////////////////////////
// Add PC's room in the palace
ACTION_IF FILE_EXISTS_IN_GAME ~BD0103.ARE~ BEGIN
	COPY_EXISTING ~BD0103.are~ ~override\#LBD0103.are~
		WRITE_ASCII (AREA_SCRIPT) ~#LPCRoom~
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~IMOEN2~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF ALTER_AREA_CONTAINER INT_VAR flag_disabled = 1 STR_VAR container_name = ~PlayerChest00~ END 
		LPF ALTER_AREA_REGION INT_VAR flag_deactivated = 1 STR_VAR region_name = ~Window_info_1~ END
		LPF ALTER_AREA_REGION INT_VAR flag_deactivated = 1 STR_VAR region_name = ~Window_info_2~ END
		LPF GW_CLONE_TRAVEL_TRIGGER STR_VAR GW_Name_old = ~TranBD0100a~ GW_Dest_new = EVAL ~%NBaldursGate_DucalPalace_L2%~ GW_Name_new = ~TranBG0109a~ GW_Entre_new = ~Exit0110a~ END
		LPF GW_CLONE_TRAVEL_TRIGGER STR_VAR GW_Name_old = ~TranBD0100b~ GW_Dest_new = EVAL ~%NBaldursGate_DucalPalace_L2%~ GW_Name_new = ~TranBG0109b~ GW_Entre_new = ~Exit0110b~ END
		LPF ALTER_AREA_REGION INT_VAR flag_deactivated = 0 STR_VAR region_name = ~TranBG0109a~ END
		LPF ALTER_AREA_REGION INT_VAR flag_deactivated = 0 STR_VAR region_name = ~TranBG0109b~ END
END ELSE ACTION_IF FILE_EXISTS_IN_GAME ~%NBaldursGate_DucalPalace_L3%.are~ BEGIN
	COPY_EXISTING ~%NBaldursGate_DucalPalace_L3%.are~ ~override\#LBD0103.are~
		WRITE_ASCII (AREA_SCRIPT) ~#LPCRoom~
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Flaming Fist Mercena~ END
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Flaming Fist Mercena~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF CHANGE_CONTAINER_NAME STR_VAR old_name = ~Container 1~ new_name = ~PlayerChest00~ END
		LPF ALTER_AREA_CONTAINER INT_VAR flag_disabled = 1 STR_VAR container_name = ~PlayerChest00~ END 
		LPF CHANGE_CONTAINER_NAME STR_VAR old_name = ~Container 4~ new_name = ~Imoen_equipment~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0109a~ new_name = ~TranBG0109a~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0109b~ new_name = ~TranBG0109b~ END
END

// Place the servant in the custom version of the third floor
// This is a common script used in all PC personal quarters
OUTER_SPRINT SOD_CHEST_TRANS ~~
ACTION_IF GAME_INCLUDES ~sod~ BEGIN
	OUTER_SPRINT SOD_CHEST_TRANS ~MoveContainerContents("BD0103*PlayerChest00","#LBD0103*PlayerChest00")~
END
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LPCRoom.baf~ EVAL

// New general script file for new palace servant
// Stops it from wandering around when first spawned
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LSrvGen.baf~

// Palace servant on the 3rd floor
COPY_EXISTING ~servan.cre~ ~override/#LDPSrv1.cre~
   WRITE_ASCII 0x280 ~#LDPSrv1~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LDPSrv1~ #8 // set dialogue file
   WRITE_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander

// Open the way to the PCs rooms
LAF UPDATE_BCS
   STR_VAR
      baf_location = EVAL ~%mod_root%/scripts/~
      baf_name = ~bg0109~
      bcs_name = ~%NBaldursGate_DucalPalace_L2%~
END

// Add 2 new inactive exit travel triggers leading from the 2nd floor to the new 3rd floor
// Activate the new ones and deactive the original ones when/if the PC and/or Imoen take up residence
COPY_EXISTING ~%NBaldursGate_DucalPalace_L2%.ARE~ ~override~
   LPF GW_CLONE_TRAVEL_TRIGGER
      INT_VAR
         GW_Flags_new = 256 // Deactivated
      STR_VAR
         GW_Name_old = ~Door0110a~
         GW_Name_new = ~Door#lbd0103a~
         GW_Dest_new = ~#LBD0103~
         GW_Entre_new = ~Exit0109a~
         GW_Key_new = ~~      // Clear out the key if one exists
         GW_Script_new = ~~   // Clear out the script if one exists
   END

   LPF GW_CLONE_TRAVEL_TRIGGER
      INT_VAR
         GW_Flags_new = 256 // Deactivated
      STR_VAR
         GW_Name_old = ~Door0110b~
         GW_Name_new = ~Door#lbd0103b~
         GW_Dest_new = ~#LBD0103~
         GW_Entre_new = ~Exit0109b~
         GW_Key_new = ~~      // Clear out the key if one exists
         GW_Script_new = ~~   // Clear out the script if one exists
   END
BUT_ONLY_IF_IT_CHANGES

// 3 Flaming Fist escort ... used in various places
COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc1.cre~
	SAY NAME1 @1007 /* ~Flaming Fist Escort~ */
	SAY NAME2 @1007 /* ~Flaming Fist Escort~ */
	WRITE_ASCII 0x280 ~#LffEsc1~ #32 // set DV
	WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
	WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
	WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc2.cre~
	SAY NAME1 @1008 // ~Flaming Fist Escort~
	SAY NAME2 @1008 // ~Flaming Fist Escort~
	WRITE_ASCII 0x280 ~#LffEsc2~ #32 // set DV
	WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
	WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
	WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc3.cre~
	SAY NAME1 @1008 // ~Flaming Fist Escort~
	SAY NAME2 @1008 // ~Flaming Fist Escort~
	WRITE_ASCII 0x280 ~#LffEsc3~ #32 // set DV
	WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
	WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
	WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

// Add an early version of the palace treasurer
ACTION_IF NOT FILE_EXISTS_IN_GAME ~BDOPHYLL.CRE~ BEGIN
	COPY ~%mod_root%/objects/#LOPHYLL.CRE~ ~override~
		SAY NAME1 ~Ophyllis~
		SAY NAME2 ~Ophyllis~
		WRITE_ASCII 0x02cc ~#LOPHYLL~ #8	// Dialogue
	EXTEND_TOP ~%NBaldursGate_DucalPalace_Cellar%.BCS~ ~%mod_root%/scripts/BG0111.baf~
END ELSE ACTION_IF NOT FILE_EXISTS_IN_GAME ~c#stophl.cre~ BEGIN // Leave jastey's version in
	COPY ~BDOPHYLL.CRE~ ~override/#LOPHYLL.CRE~
		WRITE_ASCII 0x0280 ~#LOPHYLL~ #32	// set DV
		WRITE_ASCII 0x0248 ~~ #8			// Override Script
		WRITE_ASCII 0x0268 ~#LffGen~ #8		// Default Script
		WRITE_ASCII 0x02cc ~#LOPHYLL~ #8	// Dialogue
	EXTEND_TOP ~%NBaldursGate_DucalPalace_Cellar%.BCS~ ~%mod_root%/scripts/BG0111.baf~
END

// Add an early version of the palace physician
ACTION_IF NOT FILE_EXISTS_IN_GAME ~BDFENSTE.CRE~ BEGIN
	COPY ~%mod_root%/objects/#LFENSTE.CRE~ ~override~
		SAY NAME1 ~Fenster~
		SAY NAME2 ~Fenster~
		WRITE_ASCII 0x02cc ~#LFENSTE~ #8	// Dialogue
END ELSE BEGIN
	COPY ~BDFENSTE.CRE~ ~override/#LFENSTE.CRE~
		WRITE_ASCII 0x0280 ~#LFENSTE~ #32	// set DV
		WRITE_ASCII 0x0248 ~~ #8			// Override Script
		WRITE_ASCII 0x0268 ~#LffGen~ #8		// Default Script
		WRITE_ASCII 0x02cc ~#LFENSTE~ #8	// Dialogue
END
COPY ~%mod_root%/objects/#LFenst1.STO~ ~override~
	SAY 0x000c ~Fenster~
COPY ~%mod_root%/objects/#LFenst2.STO~ ~override~
	SAY 0x000c ~Fenster~

// Dialogues for the Flaming Fist escort that are used for various components
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/ffescort.d~ USING ~%tra_loc%/%s/dialogues.tra~

// New general script file for new flaming fist healers et al
// Stops them from wandering around when first spawned
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LffGen.baf~

// Palace personnel dialogues
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/palace_personnel.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Palace gate guards
COPY_EXISTING ~flambrid.cre~ ~override/#LDPGrd1.cre~
	WRITE_ASCII 0x280 ~#LDPGrd1~ #32 // set DV
	WRITE_ASCII 0x2cc ~#LDPGrd1~ #8 // set dialogue file

COPY_EXISTING ~flambrid.cre~ ~override/#LDPGrd2.cre~
	WRITE_ASCII 0x280 ~#LDPGrd2~ #32 // set DV
	WRITE_ASCII 0x2cc ~#LDPGrd1~ #8 // set dialogue file

// Open the palace gates
// And place friendly guards
EXTEND_BOTTOM ~%NBaldursGate%.BCS~ ~%mod_root%/scripts/BG0200a.baf~

// Update Duke Belt's dialogue file
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/duke_belt.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Update Duke Liia's dialogue file
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/duke_liia.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Update Imoen's dialogue file
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/imoen_training.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Update Duke Belt's override script so he'll initiate conversation when PC first arrives in palace
EXTEND_TOP ~BELT.BCS~ ~%mod_root%/scripts/Belt_Add.baf~

// Make sure Duke Belt and Liia are alive and in position inside the Ducal Palace
// Also sets up dialogue cues
EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L1%.BCS~ ~%mod_root%/scripts/BG0108a.baf~

