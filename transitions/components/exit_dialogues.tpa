//////////////////////////////////////////////////////////////////////////////////////////////////////
// Prep stock BG1 NPCs for comments after Korlasz quest and leaving at the beginning of SoD and BG2 //
// Compiled via main_sod.tpa                                                                        //
//////////////////////////////////////////////////////////////////////////////////////////////////////

// Making this a function since there are so many variables
// This is now greatly simplified
DEFINE_ACTION_FUNCTION exit_dialogues BEGIN

	ACTION_DEFINE_ARRAY bg1_npc_list BEGIN
		Ajantis
		Jaheira
		Khalid
		Dorn
		Edwin
		Neera
		Baeloth
		Garrick
		Xan
		Dynaheir
		Minsc
		Alora
		Branwen
		Coran
		Eldoth
		Montaron
		Xzar
		Faldorn
		Kagain
		Kivan
		Quayle
		Rasaad
		Safana
		Sharteel
		Skie
		Tiax
		Viconia
		Yeslick
		Imoen
	END

	ACTION_DEFINE_ARRAY sod_npc_list BEGIN
		Jaheira
		Khalid
		Dorn
		Edwin
		Neera
		Baeloth
		Dynaheir
		Minsc
		Rasaad
		Safana
		Viconia
		Corwin
		Glint
		MKhiin
		Voghin
	END
	
	OUTER_FOR (npc_num = 0; VARIABLE_IS_SET $bg1_npc_list(~%npc_num%~); ++npc_num) BEGIN 
		OUTER_SPRINT npc_name $bg1_npc_list("%npc_num%")
		OUTER_SPRINT upper_name "%npc_name%"
		ACTION_TO_UPPER upper_name
		OUTER_SPRINT bcs_file EVAL EVAL ~%%upper_name%_BCS%~

		/////////////////////////////////////
		// BUILD THE BAF FILE FOR EACH NPC //
		/////////////////////////////////////
		// Set the NPCs up for saying goodbye or they'll be staying before moving to SoD and BG2
		// Modders can set this to 0 if they don't want them to chime in
		<<<<<<<< .../npc_add.baf
		IF
			Global("#L_SayGoodbye","LOCALS",0)
			Global("#L_SayGoodbyeSet","LOCALS",0)
		THEN
			RESPONSE #100
				SetGlobal("#L_SayGoodbye","LOCALS",1)
				SetGlobal("#L_SayGoodbyeSet","LOCALS",1)
		END
		>>>>>>>>
		EXTEND_TOP ~%bcs_file%.bcs~ ~.../npc_add.baf~ EVALUATE_BUFFER
	END // BG1 Loop

	OUTER_FOR (npc_num = 0; VARIABLE_IS_SET $sod_npc_list(~%npc_num%~); ++npc_num) BEGIN 
		OUTER_SPRINT npc_name $sod_npc_list("%npc_num%")
		OUTER_SPRINT upper_name "%npc_name%"
		ACTION_TO_UPPER upper_name
		OUTER_SPRINT bcs_file EVAL ~BD%upper_name%.BCS~
		
		ACTION_IF ~%npc_name%~ STRING_EQUAL ~Jaheira~ BEGIN
			OUTER_SPRINT bcs_file ~BDJAHEIR~
		END ELSE ACTION_IF ~%npc_name%~ STRING_EQUAL ~Baeloth~ BEGIN
			OUTER_SPRINT bcs_file ~BDBAELOT~
		END ELSE ACTION_IF ~%npc_name%~ STRING_EQUAL ~Dynaheir~ BEGIN
			OUTER_SPRINT bcs_file ~BDDYNAHE~
		END ELSE ACTION_IF ~%npc_name%~ STRING_EQUAL ~Viconia~ BEGIN
			OUTER_SPRINT bcs_file ~BDVICONI~
		END
			
		/////////////////////////////////////
		// BUILD THE BAF FILE FOR EACH NPC //
		/////////////////////////////////////
		// Set the NPCs up for saying goodbye or that they'll stay before moving to BG2
		// Modders can set this to 0 if they don't want them to chime in
		<<<<<<<< .../npc_add.baf
		IF
			Global("#L_SayGoodbye","LOCALS",0)
			Global("#L_SayGoodbyeSet","LOCALS",0)
		THEN
			RESPONSE #100
				SetGlobal("#L_SayGoodbye","LOCALS",1)
				SetGlobal("#L_SayGoodbyeSet","LOCALS",1)
		END
		>>>>>>>>
		EXTEND_TOP ~%bcs_file%.bcs~ ~.../npc_add.baf~ EVALUATE_BUFFER
	END // SoD Loop

END // Function