///////////////////////////////////////////////////////////////////////////////////////////////
// This is a straight import from jastey's Endless BG1 mod                                   //
// It's from her main component, so it'll be in the game already if that mod is loaded       //
// It wreaks havoc with some sections of my code so no matter if it's loaded here or there   //
// I'll still need to modify the files afterwards.                                           //
// The only thing we edit is we take the strings out of her GAME.TRA file and put it into    //
// endless.tra then make sure all .d files in sarevok_cleanup_more use that .tra file like:  //
// COMPILE EVALUATE_BUFFER ~.../cleanup_more.d~ USING ~%tra_loc%/%s/endless.tra~             //
///////////////////////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION town_chatter BEGIN
	ACTION_IF !(MOD_IS_INSTALLED ~c#endlessbg1/c#endlessbg1.tp2~ 0) BEGIN
		// Might be needed for imported Endless BG1 components

		ACTION_IF (GAME_IS ~bgee~ AND NOT FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN
			OUTER_SPRINT ~bgee_only~ ~~
			OUTER_SPRINT ~sod~ ~False()~
			OUTER_SPRINT ~eet_only~ ~False()~
			OUTER_SPRINT ~bdresurr~ ~sppr712~ //resurrection
			OUTER_SPRINT ~bddispel~ ~bpdispel~
			OUTER_SPRINT ~clearcld~ ~sppr318~
			OUTER_SPRINT ~BELT~ ~BELT~
			OUTER_SET BGTIMOENPState0 = 0 
			OUTER_SET BGTIMOENPState4 = 4
		END

		ACTION_IF (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN
			OUTER_SPRINT ~bgee_only~ ~False()~
			OUTER_SPRINT ~sod~ ~~
			OUTER_SPRINT ~eet_only~ ~False()~
			OUTER_SPRINT ~bdresurr~ ~bdresurr~
			OUTER_SPRINT ~bddispel~ ~bddispel~
			OUTER_SPRINT ~clearcld~ ~clearcld~
			OUTER_SPRINT ~BELT~ ~BELT~
			OUTER_SET BGTIMOENPState0 = 0 
			OUTER_SET BGTIMOENPState4 = 4
		END

		ACTION_IF (GAME_IS ~eet~) THEN BEGIN
			OUTER_SPRINT ~bgee_only~ ~False()~
			OUTER_SPRINT ~sod~ ~~
			OUTER_SPRINT ~eet_only~ ~~
			OUTER_SPRINT ~bdresurr~ ~bdresurr~
			OUTER_SPRINT ~bddispel~ ~bddispel~
			OUTER_SPRINT ~clearcld~ ~clearcld~
			OUTER_SPRINT ~BELT~ ~BELT~
			OUTER_SET BGTIMOENPState0 = 0 
			OUTER_SET BGTIMOENPState4 = 4
		END

		// From her general.tpa - it's used all over
		// We'll have to undo this for certain NPCs (like Denkod)
		/* define script for cres without DV: DestroySelf() after Sarevok's death */
		<<<<<<<< .../c#stdstr.baf

		IF
			GlobalGT("SarevokBehavior","GLOBAL",0)
			OR(2)
				Dead("Sarevok")
				!AreaCheck("%ThievesMaze%")
		THEN
			RESPONSE #100
				DestroySelf()
		END

		>>>>>>>>

		COMPILE EVALUATE_BUFFER ~.../c#stdstr.baf~

		// Her files that are brought over
		INCLUDE ~%mod_root%/components/sarevok_cleanup.tpa~
		INCLUDE ~%mod_root%/components/sarevok_cleanup_more.tpa~

		// Fix folks if cleanup_maze is loaded
		ACTION_IF (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-MAZE")) THEN BEGIN
			INCLUDE ~%mod_root%/components/fix_maze.tpa~
		END

		// Set global that will switch on post-Sarevok chatter
		EXTEND_BOTTOM ~%NBaldursGate_DucalPalace_L1%.BCS~ ~%mod_root%/scripts/hero_bg.baf~
	END
END
