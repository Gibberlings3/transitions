//////////////////////////////////////////////////////////////////
// Optional component that adds the house across from FF HQ to  //
// the list of properties available as a reward from Duke Eltan //
// Item strings are in game.tra                                 //
//////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION eltan_house3 BEGIN
	ACTION_IF !((MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_BG_N")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_HEDGE")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_BEREGOST_NW"))) BEGIN
		INCLUDE ~%mod_root%/components/eltan_common.tpa~
	END

	// Add the key he gives to the protagonist for their new house in SW Baldur's Gate
	COPY_EXISTING ~TTKey01.itm~ ~override/#LKey04.itm~
		SAY NAME1 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY NAME2 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY UNIDENTIFIED_DESC @1047
		SAY IDENTIFIED_DESC @1047
		WRITE_SHORT (0x0018) 44 // Flags
		WRITE_SHORT (0x0034) 10000 // Price
		WRITE_ASCII (0x003a) ~IMISC93~
		WRITE_ASCII (0x0058) ~GMISC93~

	COPY_EXISTING ~%SWBaldursGate%.ARE~ ~override~
		// Clone the travel trigger leading into the house so we can 
		// have the door lead to a custom area
		// One acts as a locked door when they don't have the key
		LPF GW_CLONE_TRAVEL_TRIGGER
			INT_VAR
				GW_Flags_new = 256 // Deactivated
			STR_VAR
				GW_Name_old = ~Door1107~
				GW_Name_new = ~Open#LBG1107~
				GW_Dest_new = ~#LBG1107~
				GW_Key_new = ~~
				GW_Script_new = ~~
		END
		LPF GW_CLONE_TRAVEL_TRIGGER
			INT_VAR
				GW_Flags_new = 256 // Deactivated
			STR_VAR
				GW_Name_old = ~Door1107~
				GW_Name_new = ~Locked#LBG1107~
				GW_Dest_new = ~~
				GW_Key_new = ~~ 
				GW_Script_new = ~#LKey04~
		END
	BUT_ONLY_IF_IT_CHANGES
	
	ACTION_IF GAME_INCLUDES ~sod~ THEN BEGIN
		COPY_EXISTING ~BD0030.ARE~ ~override~
			// Add travel triggers to house in the SoD version of the area
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 2
					ab_RT_Fbit = 256 
					ab_RT_BbLX = 790
					ab_RT_BbLY = 930
					ab_RT_BbHX = 900
					ab_RT_BbHY = 1050
					ab_RT_VxPr = 4
					ab_RT_Vx_X_0 = 790
					ab_RT_Vx_Y_0 = 950
					ab_RT_Vx_X_1 = 850
					ab_RT_Vx_Y_1 = 930
					ab_RT_Vx_X_2 = 900
					ab_RT_Vx_Y_2 = 1000
					ab_RT_Vx_X_3 = 825
					ab_RT_Vx_Y_3 = 1050
					ab_RT_CuId = 30
				STR_VAR
					ab_RT_Name = "Locked#LBG1107"
					ab_RT_Rbcs = "#LKey04"
			END
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 2
					ab_RT_Fbit = 256 
					ab_RT_BbLX = 790
					ab_RT_BbLY = 930
					ab_RT_BbHX = 900
					ab_RT_BbHY = 1050
					ab_RT_VxPr = 4
					ab_RT_Vx_X_0 = 790
					ab_RT_Vx_Y_0 = 950
					ab_RT_Vx_X_1 = 850
					ab_RT_Vx_Y_1 = 930
					ab_RT_Vx_X_2 = 900
					ab_RT_Vx_Y_2 = 1000
					ab_RT_Vx_X_3 = 825
					ab_RT_Vx_Y_3 = 1050
					ab_RT_CuId = 30
				STR_VAR
					ab_RT_Name = "Open#LBG1107"
					ab_RT_Dest = "#LBG1107"
					ab_RT_EntN = "Exit1100"
			END
			// Add an entrance from the house to the SoD exterior
			LPF fj_are_structure
				INT_VAR
					fj_loc_x = 630
					fj_loc_y = 950
					fj_orientation = 6
				STR_VAR
					fj_structure_type = "entrance"
					fj_name = "#Lfrom_House"
			END
		BUT_ONLY_IF_IT_CHANGES
	END

	// Also need to add a new area that can be attached to the door
	// if/when it is chosen as a reward
	COPY_EXISTING ~%SWBaldursGate_House2_L1%.are~ ~override/#LBG1107.are~
		WRITE_ASCII (AREA_SCRIPT) ~#LPCRoom~
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Male Townsfolk~ END
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Female Townsfolk~ END
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Girl~ END
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Girl~ END
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Girl~ END
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Girl~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF CHANGE_CONTAINER_NAME STR_VAR old_name = ~Container 1~ new_name = ~PlayerChest00~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door1108~ destination_area = ~#LBG1108~ END
		
		// Add a travel region that leads out to the SoD version of the exterior
		LPF GW_CLONE_TRAVEL_TRIGGER
			INT_VAR
				GW_Flags_new = 256 // Deactivated
			STR_VAR
				GW_Name_old = ~Door1100~
				GW_Name_new = ~DoorSod~
				GW_Dest_new = ~BD0030~
				GW_Entre_new = ~#Lfrom_House~
		END
	COPY_EXISTING ~%SWBaldursGate_House2_L2%.are~ ~override/#LBG1108.are~
		WRITE_ASCII (AREA_SCRIPT) ~~ (8)
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Sleeping Man~ END
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Sleeping Woman~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door1107~ destination_area = ~#LBG1107~ END
		
	// Update the script used on all PC residences to include the one in front of the ducal palace
	OUTER_SPRINT SOD_CHEST_TRANS ~~
	OUTER_SPRINT BG2_CHEST_TRANS ~~
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		OUTER_SPRINT SOD_CHEST_TRANS ~MoveContainerContents("BD0103*PlayerChest00","#LBG1107*PlayerChest00") MoveContainerContents("BD1000*PlayerChest00","#LBG1107*PlayerChest00") MoveContainerContents("BD7100*PlayerChest00","#LBG1107*PlayerChest00") MoveContainerContents("BD3000*PlayerChest00","#LBG1107*PlayerChest00")~

		ACTION_IF GAME_IS ~eet~ BEGIN
			OUTER_SPRINT BG2_CHEST_TRANS ~MoveContainerContents("AR0523*Container4","#LBG1107*PlayerChest00") MoveContainerContents("AR1306*container11","#LBG1107*PlayerChest00") MoveContainerContents("AR1107*#LPlayerChest","#LBG1107*PlayerChest00") MoveContainerContents("AR0322*container5","#LBG1107*PlayerChest00") MoveContainerContents("AR0411*#LPlayerChest","#LBG1107*PlayerChest00") MoveContainerContents("AR1902*#LPlayerChest","#LBG1107*PlayerChest00") MoveContainerContents("AR0901*#LPlayerChest","#LBG1107*PlayerChest00") MoveContainerContents("AR0902*#LPlayerChest","#LBG1107*PlayerChest00") MoveContainerContents("AR0903*#LPlayerChest","#LBG1107*PlayerChest00") MoveContainerContents("AR0904*#LPlayerChest","#LBG1107*PlayerChest00")~
		END
	END
	COPY_EXISTING ~#LPCRoom.BCS~ ~override~
	   DECOMPILE_AND_PATCH BEGIN 
			// Move the contents from this house's chest to the ducal palace chest when they go there
			REPLACE_TEXTUALLY ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1)~ ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1) MoveContainerContents("#LBG1107*PlayerChest00","#LBD0103*PlayerChest00")~

			// Add the block where the SoD ducal palace chest contents get move to the house
			APPEND_FILE TEXT ~%mod_root%/scripts/#LPCRoomd.baf~ EVAL
		END
	BUT_ONLY_IF_IT_CHANGES

	// Prep PC's house in SW BG
	LAF UPDATE_BCS
		STR_VAR 
			baf_location = EVAL ~%mod_root%/scripts/~
			baf_name = ~BG1100a~
			bcs_name = EVAL ~%SWBaldursGate%~
	END
	LAF UPDATE_BCS
		STR_VAR 
			baf_location = EVAL ~%mod_root%/scripts/~
			baf_name = ~BG1100a~
			bcs_name = ~BD0030~
	END
	
	// Set up script that emulates a door
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LKey04.baf~ USING ~%tra_loc%/%s/dialogues.tra~

	// Update the dialogue of the servant who welcomes them
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/house3_servant.d~ USING ~%tra_loc%/%s/dialogues.tra~

	// Set the global to indicate that the house is available as a reward
	EXTEND_TOP ~%NBaldursGate_DucalPalace_L1%.BCS~ ~%mod_root%/scripts/BG0108f2.baf~
END
