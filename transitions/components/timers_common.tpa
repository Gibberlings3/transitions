/////////////////////////////////////////////////////////////////////
// Component that adds world event timers to auto-advance the game //
/////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION timers_common BEGIN

	///////////////////////////////////////
	// Timers only used for refugees now //
	///////////////////////////////////////
	
	ACTION_IF !(MOD_IS_INSTALLED ~c#endlessbg1.tp2~ (ID_OF_LABEL "c#endlessbg1.tp2" "EBG1_RefugeesInBGCity")) BEGIN
		// Area script to set up timers
		EXTEND_BOTTOM ~%Undercity_TempleofBhaal%.BCS~ ~%mod_root%/scripts/BG0125_timers.baf~

		// Put timer checks in BALDUR.BCS
		EXTEND_TOP ~BALDUR.BCS~ ~%mod_root%/scripts/timer_checks.baf~

		// Refugee CREs (taken directly from jastey's EBG1 except for naming)
		COPY_EXISTING ~FTOBE2.cre~ ~override/#LRfgF01.cre~ // Female #1
			SAY NAME1 @1029 /* Refugee */
			SAY NAME2 @1029 /* Refugee */
			WRITE_ASCII 0x280 ~#LRfgF01~ #32 // set DV
			WRITE_ASCII 0x2cc ~#LRfgAdt~ #8 // set dialogue file
			WRITE_EVALUATED_ASCII 0x268 ~~ #8   // Default script

		COPY_EXISTING ~FTOBE5.cre~ ~override/#LRfgF02.cre~ // Female #2
			SAY NAME1 @1029 /* Refugee */
			SAY NAME2 @1029 /* Refugee */
			WRITE_ASCII 0x280 ~#LRfgF02~ #32 // set DV
			WRITE_ASCII 0x2cc ~#LRfgAdt~ #8 // set dialogue file
			WRITE_EVALUATED_ASCII 0x268 ~~ #8   // Default script

		COPY_EXISTING ~MTBE3.cre~ ~override/#LRfgM01.cre~ // Male #1
			SAY NAME1 @1029 /* Refugee */
			SAY NAME2 @1029 /* Refugee */
			WRITE_ASCII 0x280 ~#LRfgM01~ #32 // set DV
			WRITE_ASCII 0x2cc ~#LRfgAdt~ #8 // set dialogue file
			WRITE_EVALUATED_ASCII 0x260 ~~ #8   // General script

		COPY_EXISTING ~MTBE4.cre~ ~override/#LRfgM02.cre~ // Male #2
			SAY NAME1 @1029 /* Refugee */
			SAY NAME2 @1029 /* Refugee */
			WRITE_ASCII 0x280 ~#LRfgM02~ #32 // set DV
			WRITE_ASCII 0x2cc ~#LRfgAdt~ #8 // set dialogue file

		COPY_EXISTING ~BOYBA1.cre~ ~override/#LRfgB01.cre~	// Boy
			SAY NAME1 @1029 /* Refugee */
			SAY NAME2 @1029 /* Refugee */
			WRITE_ASCII 0x280 ~#LRfgB01~ #32 // set DV
			WRITE_ASCII 0x2cc ~#LRfgChl~ #8 // set dialogue file
			WRITE_EVALUATED_ASCII 0x268 ~~ #8   // Default script

		COPY_EXISTING ~GIRBA1.cre~ ~override/#LRfgG01.cre~	// Girl
			SAY NAME1 @1029 /* Refugee */
			SAY NAME2 @1029 /* Refugee */
			WRITE_ASCII 0x280 ~#LRfgG01~ #32 // set DV
			WRITE_ASCII 0x2cc ~#LRfgChl~ #8 // set dialogue file
			WRITE_EVALUATED_ASCII 0x268 ~~ #8   // Default script

		// Refugee dialogue
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/refugees.d~ USING ~%tra_loc%/%s/dialogues.tra~

		// Set up BG north to spawn the refugees
		EXTEND_BOTTOM ~%NBaldursGate%.BCS~ ~%mod_root%/scripts/BG0200_refugees.baf~
	END
END

