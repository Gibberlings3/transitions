//////////////////////////////////////////////////////////////////////////////
// Optional component that adds the house in north BG near the Ducal Palace //
// to the list of properties available as a reward from Duke Eltan          //
// Item strings are in game.tra                                             //
//////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION eltan_house0 BEGIN
	ACTION_IF !((MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_HEDGE")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_BEREGOST_NW")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_BG_SW"))) BEGIN
		INCLUDE ~%mod_root%/components/eltan_common.tpa~
	END

	// Add the key he gives to the protagonist for their new house in front of the Ducal Palace
	COPY_EXISTING ~TTKey01.itm~ ~override/#LKey01.itm~
		SAY NAME1 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY NAME2 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY UNIDENTIFIED_DESC @1010
		SAY IDENTIFIED_DESC @1010
		WRITE_SHORT (0x0018) 44 // Flags
		WRITE_SHORT (0x0034) 10000 // Price
		WRITE_ASCII (0x003a) ~IMISC93~
		WRITE_ASCII (0x0058) ~GMISC93~

	COPY_EXISTING ~%NBaldursGate%.ARE~ ~override~
		// Change the name of the door leading into BG0006
		// or else we can't control it because it has the same name as the region
		LPF CHANGE_DOOR_NAME
			STR_VAR
				old_name = ~Door0006~
				new_name = ~Port0006~
		END

		// Clone the travel trigger leading into the house so we can 
		// have the door lead to a custom area
		LPF GW_CLONE_TRAVEL_TRIGGER
			INT_VAR
				GW_Flags_new = 256 // Deactivated
			STR_VAR
				GW_Name_old = ~Door0006~
				GW_Name_new = ~Door#lbg0006~
				GW_Dest_new = ~#LBG0006~
				GW_Key_new = ~~      // Clear out the key if one exists
				GW_Script_new = ~~   // Clear out the script if one exists
		END

		// Need to modify the door object in BG0200 leading to the house in front 
		// of the palace in case that one is chosen as their reward
		LPF ALTER_AREA_DOOR
			INT_VAR
				lock_difficulty = 100
			STR_VAR
				door_name = ~Port0006~
				door_open_sound = ~AMB_D21D~
				door_key = ~#LKey01~
		END
	BUT_ONLY_IF_IT_CHANGES
	
	ACTION_IF GAME_INCLUDES ~sod~ THEN BEGIN
		// Add a door to the PC's residence in the SoD areas
		
		// Determine the SoD PVRz file the new BG1 graphics files will be copied to
		OUTER_SPRINT source_day "A020008"
		OUTER_SPRINT source_night "A0200N08"
		OUTER_SPRINT page1 ~-1~
		OUTER_SPRINT page2 ~-1~
		OUTER_SPRINT page3 ~-1~
		OUTER_SET strt_idx = 0
		OUTER_SET file_1_ok = 0
		OUTER_SET file_2_ok = 0
		OUTER_SET file_3_ok = 0
		ACTION_IF GAME_IS ~eet~ THEN BEGIN
			OUTER_SPRINT source_day "B020008"
			OUTER_SPRINT source_night "B0200N08"
			OUTER_SET strt_idx = 50
		END
		OUTER_FOR (idx = strt_idx; idx < 99 && file_1_ok = 0; idx = idx + 1) BEGIN
			ACTION_IF idx < 10 THEN BEGIN
				OUTER_SPRINT page1 ~0%idx%~
			END ELSE BEGIN
				OUTER_SPRINT page1 ~%idx%~
			END
			ACTION_IF NOT FILE_EXISTS_IN_GAME ~B0010%page1%.pvrz~ THEN BEGIN
				OUTER_SET file_1_ok = 1
			END
		END
		OUTER_FOR (idx = strt_idx; idx < 99 && file_2_ok = 0; idx = idx + 1) BEGIN
			ACTION_IF idx < 10 THEN BEGIN
				OUTER_SPRINT page2 ~0%idx%~
			END ELSE BEGIN
				OUTER_SPRINT page2 ~%idx%~
			END
			ACTION_IF NOT FILE_EXISTS_IN_GAME ~B0010N%page2%.pvrz~ THEN BEGIN
				OUTER_SET file_2_ok = 1
			END
		END
		OUTER_FOR (idx = strt_idx; idx < 99 && file_3_ok = 0; idx = idx + 1) BEGIN
			ACTION_IF idx < 10 THEN BEGIN
				OUTER_SPRINT page3 ~0%idx%~
			END ELSE BEGIN
				OUTER_SPRINT page3 ~%idx%~
			END
			ACTION_IF NOT FILE_EXISTS_IN_GAME ~B0101%page3%.pvrz~ THEN BEGIN
				OUTER_SET file_3_ok = 1
			END
		END
		
		ACTION_IF file_1_ok = 1 AND file_2_ok AND file_3_ok = 1 THEN BEGIN	// This is sort of overkill... but ya never know
			COPY_EXISTING ~%source_day%.PVRZ~ ~override\B0010%page1%.pvrz~
			COPY_EXISTING ~%source_night%.PVRZ~ ~override\B0010N%page2%.pvrz~
			COPY_EXISTING ~%source_day%.PVRZ~ ~override\B0101%page3%.pvrz~

			COPY_EXISTING ~BD0010.tis~ ~override~
				// Add closed door tiles to end of TIS
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 0 PVRz_Y = 448 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 64 PVRz_Y = 448 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 0 PVRz_Y = 512 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 64 PVRz_Y = 512 STR_VAR Method = "Push" RET Count END

				// Replace original closed door tiles with open door tiles from BG1
				LPF ps_tileset_add_tiles INT_VAR Pos = 140 MaxCount = 1 PVRz_Page = %page1% PVRz_X = 128 PVRz_Y = 704 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 141 MaxCount = 1 PVRz_Page = %page1% PVRz_X = 192 PVRz_Y = 704 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 160 MaxCount = 1 PVRz_Page = %page1% PVRz_X = 128 PVRz_Y = 768 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 161 MaxCount = 1 PVRz_Page = %page1% PVRz_X = 192 PVRz_Y = 768 STR_VAR Method = "Replace" RET Count END
			BUT_ONLY_IF_IT_CHANGES

			COPY_EXISTING ~BD0010N.tis~ ~override~
				// Add closed door tiles to end of TIS
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 0 PVRz_Y = 448 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 64 PVRz_Y = 448 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 0 PVRz_Y = 512 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 64 PVRz_Y = 512 STR_VAR Method = "Push" RET Count END

				// Replace original closed door tiles with open door tiles from BG1
				LPF ps_tileset_add_tiles INT_VAR Pos = 140 MaxCount = 1 PVRz_Page = %page2% PVRz_X = 128 PVRz_Y = 704 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 141 MaxCount = 1 PVRz_Page = %page2% PVRz_X = 192 PVRz_Y = 704 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 160 MaxCount = 1 PVRz_Page = %page2% PVRz_X = 128 PVRz_Y = 768 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 161 MaxCount = 1 PVRz_Page = %page2% PVRz_X = 192 PVRz_Y = 768 STR_VAR Method = "Replace" RET Count END
			BUT_ONLY_IF_IT_CHANGES

			COPY_EXISTING ~BD0101.tis~ ~override~
				// Add closed door tiles to end of TIS
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 0 PVRz_Y = 448 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 64 PVRz_Y = 448 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 0 PVRz_Y = 512 STR_VAR Method = "Push" RET Count END
				LPF ps_tileset_add_tiles INT_VAR MaxCount = 1 PVRz_Page = "%strt_idx%" PVRz_X = 64 PVRz_Y = 512 STR_VAR Method = "Push" RET Count END

				// Replace original closed door tiles with open door tiles from BG1
				LPF ps_tileset_add_tiles INT_VAR Pos = 140 MaxCount = 1 PVRz_Page = %page3% PVRz_X = 128 PVRz_Y = 704 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 141 MaxCount = 1 PVRz_Page = %page3% PVRz_X = 192 PVRz_Y = 704 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 160 MaxCount = 1 PVRz_Page = %page3% PVRz_X = 128 PVRz_Y = 768 STR_VAR Method = "Replace" RET Count END
				LPF ps_tileset_add_tiles INT_VAR Pos = 161 MaxCount = 1 PVRz_Page = %page3% PVRz_X = 192 PVRz_Y = 768 STR_VAR Method = "Replace" RET Count END
			BUT_ONLY_IF_IT_CHANGES
		END

		COPY_EXISTING ~BD0010.ARE~ ~override~
			// Add a door object leading to PC's house when in SoD
		
			// Add a travel trigger leading into the PC's house
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 2
					ab_RT_Fbit = 256 
					ab_RT_BbLX = 39
					ab_RT_BbLY = 460
					ab_RT_BbHX = 84
					ab_RT_BbHY = 536
					ab_RT_VxPr = 4
					ab_RT_Vx_X_0 = 39
					ab_RT_Vx_Y_0 = 524
					ab_RT_Vx_X_1 = 83
					ab_RT_Vx_Y_1 = 536
					ab_RT_Vx_X_2 = 84
					ab_RT_Vx_Y_2 = 473
					ab_RT_Vx_X_3 = 39
					ab_RT_Vx_Y_3 = 460
					ab_RT_CuId = 30
				STR_VAR
					ab_RT_Name = "Door#lbg0006"
					ab_RT_Dest = "#LBG0006"
					ab_RT_EntN = "Exit0200"
			END
			
			// Add an entrance from the house to the SoD exteriors
			LPF fj_are_structure
				INT_VAR
					fj_loc_x = 135
					fj_loc_y = 625
					fj_orientation = 6
				STR_VAR
					fj_structure_type = "entrance"
					fj_name = "#Lfrom_House"
			END
		BUT_ONLY_IF_IT_CHANGES
		
		COPY_EXISTING ~BD0101.ARE~ ~override~
			// Add a door object leading to PC's house when in SoD
		
			// Add a travel trigger leading into the PC's house
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 2
					ab_RT_Fbit = 256 
					ab_RT_BbLX = 39
					ab_RT_BbLY = 460
					ab_RT_BbHX = 84
					ab_RT_BbHY = 536
					ab_RT_VxPr = 4
					ab_RT_Vx_X_0 = 39
					ab_RT_Vx_Y_0 = 524
					ab_RT_Vx_X_1 = 83
					ab_RT_Vx_Y_1 = 536
					ab_RT_Vx_X_2 = 84
					ab_RT_Vx_Y_2 = 473
					ab_RT_Vx_X_3 = 39
					ab_RT_Vx_Y_3 = 460
					ab_RT_CuId = 30
				STR_VAR
					ab_RT_Name = "Door#lbg0006"
					ab_RT_Dest = "#LBG0006"
					ab_RT_EntN = "Exit0200"
			END
			
			// Add an entrance from the house to the SoD exteriors
			LPF fj_are_structure
				INT_VAR
					fj_loc_x = 135
					fj_loc_y = 625
					fj_orientation = 6
				STR_VAR
					fj_structure_type = "entrance"
					fj_name = "#Lfrom_House"
			END
		BUT_ONLY_IF_IT_CHANGES		
	END

	// Also need to add a new area that can be attached to the door
	// if/when it is chosen as a reward
	COPY_EXISTING ~%NWBaldursGate_House3_L1%.are~ ~override/#LBG0006.are~
		WRITE_ASCII (AREA_SCRIPT) ~#LPCRoom~
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Male Townsfolk~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF CHANGE_CONTAINER_NAME STR_VAR old_name = ~Container 1~ new_name = ~PlayerChest00~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0100~ new_name = ~Door0200~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0156~ new_name = ~Door0007~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0100~ new_name = ~Exit0200~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0156~ new_name = ~Exit0007~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door0007~ destination_area = ~#LBG0007~ entrance_name = ~Exit0006~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door0200~ destination_area = EVAL ~%NBaldursGate%~ entrance_name = ~Exit0006~ END
		
		PATCH_IF GAME_INCLUDES ~sod~ THEN BEGIN
			// Add regions leading out to the SoD exteriors
			LPF GW_CLONE_TRAVEL_TRIGGER
				INT_VAR
					GW_Flags_new = 256 // Deactivated
				STR_VAR
					GW_Name_old = ~Door0200~
					GW_Name_new = ~DoorSoD1~
					GW_Dest_new = ~BD0010~
					GW_Entre_new = ~#Lfrom_House~
			END
		
			LPF GW_CLONE_TRAVEL_TRIGGER
				INT_VAR
					GW_Flags_new = 256 // Deactivated
				STR_VAR
					GW_Name_old = ~Door0200~
					GW_Name_new = ~DoorSoD2~
					GW_Dest_new = ~BD0101~
					GW_Entre_new = ~#Lfrom_House~
			END
		END
		
	COPY_EXISTING ~%NWBaldursGate_House2_L2%.are~ ~override/#LBG0007.are~
		WRITE_ASCII (AREA_SCRIPT) ~~ (8)
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Sleeping Man~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0163~ new_name = ~Door#LBG0006~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0163~ new_name = ~Exit0006~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door#LBG0006~ destination_area = ~#LBG0006~ entrance_name = ~Exit0007~ END

	// Update the script used on all PC residences to include the one in front of the ducal palace
	OUTER_SPRINT SOD_CHEST_TRANS ~~
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		OUTER_SPRINT SOD_CHEST_TRANS ~MoveContainerContents("BD0103*PlayerChest00","#LBG0006*PlayerChest00")~
	END
	COPY_EXISTING ~#LPCRoom.BCS~ ~override~
	   DECOMPILE_AND_PATCH BEGIN 
			// Move the contents from this house's chest to the ducal palace chest when they go there
			REPLACE_TEXTUALLY ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1)~ ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1) MoveContainerContents("#LBG0006*PlayerChest00","#LBD0103*PlayerChest00")~

			// Add the block where the ducal palace chest contents get move to the house
			APPEND_FILE TEXT ~%mod_root%/scripts/#LPCRooma.baf~ EVAL
		END
	BUT_ONLY_IF_IT_CHANGES

	// Prep PC's house in front of the Ducal Palace
	EXTEND_BOTTOM ~%NBaldursGate%.BCS~ ~%mod_root%/scripts/BG0200c.baf~ EVAL

	// If PC ever meets Rinnie in the original BG0006, set her up as
	// a global so we can try to move her if the PC picks her house
	// as their residence
	LAF UPDATE_BCS
		STR_VAR
			baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
			baf_name = ~BG0006~			// like my_new_BG0146
			bcs_name = EVAL ~%NBaldursGate_RinniesHouse_L1%~			// like BG0146
	END

	// Update the dialogue of the servant who welcomes them
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/house0_servant.d~ USING ~%tra_loc%/%s/dialogues.tra~

	// Set the global to indicate that the house in north Baldur's Gate is available as a reward
	EXTEND_TOP ~%NBaldursGate_DucalPalace_L1%.BCS~ ~%mod_root%/scripts/bg0108f.baf~
END