//////////////////////////////////////////////////////////////////////////////
// Optional component that adds the house in north BG near the Ducal Palace //
// to the list of properties available as a reward from Duke Eltan          //
// Item strings are in game.tra                                             //
//////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION eltan_house0 BEGIN
	ACTION_IF !((MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_HEDGE")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_BEREGOST_NW")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_BG_SW"))) BEGIN
		INCLUDE ~%mod_root%/components/eltan_common.tpa~
	END

	// Add the key he gives to the protagonist for their new house in front of the Ducal Palace
	COPY_EXISTING ~TTKey01.itm~ ~override/#LKey01.itm~
		SAY NAME1 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY NAME2 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY UNIDENTIFIED_DESC @1010
		SAY IDENTIFIED_DESC @1010
		WRITE_SHORT (0x0018) 44 // Flags
		WRITE_SHORT (0x0034) 10000 // Price
		WRITE_ASCII (0x003a) ~IMISC93~
		WRITE_ASCII (0x0058) ~GMISC93~

	COPY_EXISTING ~%NBaldursGate%.ARE~ ~override~
		// Change the name of the door leading into BG0006
		// or else we can't control it because it has the same name as the region
		LPF CHANGE_DOOR_NAME
			STR_VAR
				old_name = ~Door0006~
				new_name = ~Port0006~
		END

		// Clone the travel trigger leading into the house so we can 
		// have the door lead to a custom area
		LPF CLONE_TRAVEL_TRIGGER
			INT_VAR
				flags_new = 256 // Deactivated
			STR_VAR
				name_old = ~Door0006~
				name_new = ~Door#lbg0006~
				dest_new = ~#LBG0006~
				key_new = ~~      // Clear out the key if one exists
				script_new = ~~   // Clear out the script if one exists
		END

		// Need to modify the door object in BG0200 leading to the house in front 
		// of the palace in case that one is chosen as their reward
		LPF ALTER_AREA_DOOR
			INT_VAR
				lock_difficulty = 100
			STR_VAR
				door_name = ~Port0006~
				door_open_sound = ~AMB_D21D~
				door_key = ~#LKey01~
		END
	BUT_ONLY_IF_IT_CHANGES
	
	ACTION_IF GAME_INCLUDES ~sod~ THEN BEGIN
		// Add a door to the PC's residence in the SoD areas
		// Will take the graphic for the open door from the BG1 area PVRz files
		// There are 4 versions of every exterior door - open and closed during the day and night
		// Using BG0200 X - 1151 for SoD X vertices
		// Using BG0200 Y - 2306 for SoD Y vertices
		
		OUTER_SPRINT open_source_day "A020008"
		OUTER_SPRINT open_source_night "A0200N08"
		OUTER_SPRINT closed_source_day "B001000"
		OUTER_SPRINT closed_source_night "B0010N00"
		ACTION_IF GAME_IS ~eet~ THEN BEGIN
			OUTER_SPRINT open_source_day "B020008"
			OUTER_SPRINT open_source_night "B0200N08"
			OUTER_SPRINT closed_source_day "B001050"
			OUTER_SPRINT closed_source_night "B0010N50"
		END

		// Daytime door for initial SoD area
		LAF ADD_DOOR
			INT_VAR
				is_day = 1
				g_closed_x1 = 0		// X,Y coordinates from within the source PVRz file
				g_closed_x2 = 64
				g_closed_y1 = 448
				g_closed_y2 = 512
				g_open_x1 = 128
				g_open_x2 = 192
				g_open_y1 = 704
				g_open_y2 = 768
				t_open_pos1 = 140		// The tile # in the target TIS to be replaced
				t_open_pos2 = 141
				t_open_pos3 = 160
				t_open_pos4 = 161
				v_closed_x0 = 39				// X/Y pairs (vertices) of the actual door object - only allowing 4 per door state
				v_closed_y0 = 524				// Must be given in clockwise order starting with the one furthest to the right
				v_closed_x1 = 87				// If 2 tie for rightmost - use the lower of the two
				v_closed_y1 = 535
				v_closed_x2 = 87
				v_closed_y2 = 469
				v_closed_x3 = 39
				v_closed_y3 = 457
				v_open_x0 = 16			
				v_open_y0 = 553			
				v_open_x1 = 39			
				v_open_y1 = 522
				v_open_x2 = 39
				v_open_y2 = 455
				v_open_x3 = 16
				v_open_y3 = 484
			STR_VAR
				source_open_pvrz = EVAL "%open_source_day%"
				source_closed_pvrz = EVAL "%closed_source_day%"
				target_tis = ~BD0010~
				target_wed = ~BD0010~
				target_are = ~BD0010~
				door_name = "Port0006"
				door_id = "door0006"
			RET 
				pvrzOpenDay = open_pvrz
				pvrzClosedDay = closed_pvrz
		END
		
		// Nighttime door for initial SoD area
		LAF ADD_DOOR
			INT_VAR
				is_day = 0
				g_closed_x1 = 0			// X,Y coordinates from within the source PVRz file (use DLTCEP to determine)
				g_closed_x2 = 64
				g_closed_y1 = 448
				g_closed_y2 = 512
				g_open_x1 = 128		
				g_open_x2 = 192
				g_open_y1 = 704
				g_open_y2 = 768
				t_open_pos1 = 140			// The tile # in the target TIS to be replaced
				t_open_pos2 = 141
				t_open_pos3 = 160
				t_open_pos4 = 161
				v_closed_x0 = 39				// X/Y pairs (vertices) of the actual door object - only allowing 4 per door state
				v_closed_y0 = 524				// Must be given in clockwise order starting with the one furthest to the right
				v_closed_x1 = 87				// If 2 tie for rightmost - use the lower of the two
				v_closed_y1 = 535
				v_closed_x2 = 87
				v_closed_y2 = 469
				v_closed_x3 = 39
				v_closed_y3 = 457
				v_open_x0 = 16			
				v_open_y0 = 553			
				v_open_x1 = 39			
				v_open_y1 = 522
				v_open_x2 = 39
				v_open_y2 = 455
				v_open_x3 = 16
				v_open_y3 = 484
			STR_VAR
				source_open_pvrz = EVAL "%open_source_night%"
				source_closed_pvrz = EVAL "%closed_source_night%"
				target_tis = ~BD0010N~
				target_wed = ~BD0010N~
				target_are = ~BD0010~
				door_name = "Port0006"
				door_id = "door0006"
			RET 
				pvrzOpenNight = open_pvrz
				pvrzClosedNight = closed_pvrz
		END
		
		// Daytime door for 2nd SoD area - this area only has day-time graphics
		LAF ADD_DOOR
			INT_VAR
				is_day = 1
				g_closed_x1 = 0		// X,Y coordinates from within the source PVRz file
				g_closed_x2 = 64
				g_closed_y1 = 448
				g_closed_y2 = 512
				g_open_x1 = 128
				g_open_x2 = 192
				g_open_y1 = 704
				g_open_y2 = 768
				t_open_pos1 = 140		// The tile # in the target TIS to be replaced
				t_open_pos2 = 141
				t_open_pos3 = 160
				t_open_pos4 = 161
				v_closed_x0 = 39				// X/Y pairs (vertices) of the actual door object - only allowing 4 per door state
				v_closed_y0 = 524				// Must be given in clockwise order starting with the one furthest to the right
				v_closed_x1 = 87				// If 2 tie for rightmost - use the lower of the two
				v_closed_y1 = 535
				v_closed_x2 = 87
				v_closed_y2 = 469
				v_closed_x3 = 39
				v_closed_y3 = 457
				v_open_x0 = 16			
				v_open_y0 = 553			
				v_open_x1 = 39			
				v_open_y1 = 522
				v_open_x2 = 39
				v_open_y2 = 455
				v_open_x3 = 16
				v_open_y3 = 484
			STR_VAR
				source_open_pvrz = EVAL "%open_source_day%"
				source_closed_pvrz = EVAL "%closed_source_day%"
				target_tis = ~BD0101~
				target_wed = ~BD0101~
				target_are = ~BD0101~
				door_name = "Port0006"
				door_id = "door0006"
			RET 
				pvrzOpenDay = open_pvrz
				pvrzClosedDay = closed_pvrz
		END
		
		// Add regions and entrance objects and modify the new doors in the SoD area files
		COPY_EXISTING ~BD0010.ARE~ ~override~
			// Add an entrance from the house to the SoD exteriors
			LPF fj_are_structure
				INT_VAR
					fj_loc_x = 135
					fj_loc_y = 625
					fj_orientation = 6
				STR_VAR
					fj_structure_type = "entrance"
					fj_name = "#Lfrom_House"
			END
			
			// Change the current info trigger into a travel trigger
			LPF ALTER_AREA_REGION
				INT_VAR
					type = 2
					cursor = 30
					flag_connect_to_door = 1
					flag_activation_point = 1
					info_point = ~-1~
					launch_x = 51
					launch_y = 566
					activate_x = 51
					activate_y = 566
				STR_VAR
					region_name = ~Infopoint08~
					destination_area = ~#LBG0006~
					entrance_name = "Exit0200"
			END
			
			// Update the door object
			LPF ALTER_AREA_DOOR
				INT_VAR
					flag_locked = 1
					lock_difficulty = 100
					launch_x = 51
					launch_y = 566
					open_x = 48
					open_y = 570
					close_x = 48
					close_y = 570
				STR_VAR
					door_name = ~Port0006~
					travel_trigger = ~Infopoint08~
					door_open_sound = ~AMB_D21D~
					door_key = ~#LKey01~
			END
		BUT_ONLY_IF_IT_CHANGES

		COPY_EXISTING ~BD0101.ARE~ ~override~
			// Add an entrance from the house to the SoD exteriors
			LPF fj_are_structure
				INT_VAR
					fj_loc_x = 135
					fj_loc_y = 625
					fj_orientation = 6
				STR_VAR
					fj_structure_type = "entrance"
					fj_name = "#Lfrom_House"
			END
		
			// Add a travel trigger leading into the PC's house
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 2
					ab_RT_Fbit = 3072		// connected to door and use alternate activation point
					ab_RT_BbLX = 39
					ab_RT_BbLY = 460
					ab_RT_BbHX = 84
					ab_RT_BbHY = 536
					ab_RT_VxPr = 4
					ab_RT_Vx_X_0 = 39
					ab_RT_Vx_Y_0 = 524
					ab_RT_Vx_X_1 = 83
					ab_RT_Vx_Y_1 = 536
					ab_RT_Vx_X_2 = 84
					ab_RT_Vx_Y_2 = 473
					ab_RT_Vx_X_3 = 39
					ab_RT_Vx_Y_3 = 460
					ab_RT_CuId = 30
					ab_RT_LPoX = 51
					ab_RT_LPoY = 566
					ab_RT_ALPX = 51
					ab_RT_ALPY = 566
				STR_VAR
					ab_RT_Name = "Door#lbg0006"
					ab_RT_Dest = "#LBG0006"
					ab_RT_EntN = "Exit0200"
			END

			// Update the door object
			LPF ALTER_AREA_DOOR
				INT_VAR
					flag_locked = 1
					lock_difficulty = 100
					launch_x = 51
					launch_y = 566
					open_x = 48
					open_y = 570
					close_x = 48
					close_y = 570
				STR_VAR
					door_name = ~Port0006~
					door_open_sound = ~AMB_D21D~
					travel_trigger = ~Door#lbg0006~
					door_key = ~#LKey01~
			END
		BUT_ONLY_IF_IT_CHANGES		
	END

	// Also need to add a new area that can be attached to the door
	// if/when it is chosen as a reward
	COPY_EXISTING ~%NWBaldursGate_House3_L1%.are~ ~override/#LBG0006.are~
		WRITE_ASCII (AREA_SCRIPT) ~#LPCRoom~
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Male Townsfolk~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF CHANGE_CONTAINER_NAME STR_VAR old_name = ~Container 1~ new_name = ~PlayerChest00~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0100~ new_name = ~Door0200~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0156~ new_name = ~Door0007~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0100~ new_name = ~Exit0200~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0156~ new_name = ~Exit0007~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door0007~ destination_area = ~#LBG0007~ entrance_name = ~Exit0006~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door0200~ destination_area = EVAL ~%NBaldursGate%~ entrance_name = ~Exit0006~ END
		
		PATCH_IF GAME_INCLUDES ~sod~ THEN BEGIN
			// Add regions leading out to the SoD exteriors
			LPF GW_CLONE_TRAVEL_TRIGGER
				INT_VAR
					GW_Flags_new = 256 // Deactivated
				STR_VAR
					GW_Name_old = ~Door0200~
					GW_Name_new = ~DoorSoD1~
					GW_Dest_new = ~BD0010~
					GW_Entre_new = ~#Lfrom_House~
			END
		
			LPF GW_CLONE_TRAVEL_TRIGGER
				INT_VAR
					GW_Flags_new = 256 // Deactivated
				STR_VAR
					GW_Name_old = ~Door0200~
					GW_Name_new = ~DoorSoD2~
					GW_Dest_new = ~BD0101~
					GW_Entre_new = ~#Lfrom_House~
			END
		END
		
	COPY_EXISTING ~%NWBaldursGate_House2_L2%.are~ ~override/#LBG0007.are~
		WRITE_ASCII (AREA_SCRIPT) ~~ (8)
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Sleeping Man~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0163~ new_name = ~Door#LBG0006~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0163~ new_name = ~Exit0006~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door#LBG0006~ destination_area = ~#LBG0006~ entrance_name = ~Exit0007~ END

	OUTER_SPRINT SOD_CHEST_TRANS ~~
	OUTER_SPRINT BG2_CHEST_TRANS ~~
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		OUTER_SPRINT SOD_CHEST_TRANS ~MoveContainerContents("BD0103*PlayerChest00","#LBG0006*PlayerChest00") MoveContainerContents("BD1000*PlayerChest00","#LBG0006*PlayerChest00") MoveContainerContents("BD7100*PlayerChest00","#LBG0006*PlayerChest00") MoveContainerContents("BD3000*PlayerChest00","#LBG0006*PlayerChest00")~
		
		ACTION_IF GAME_IS ~eet~ BEGIN
			OUTER_SPRINT BG2_CHEST_TRANS ~MoveContainerContents("AR0523*Container4","#LBG0006*PlayerChest00") MoveContainerContents("AR1306*container11","#LBG0006*PlayerChest00") MoveContainerContents("AR1107*#LPlayerChest","#LBG0006*PlayerChest00") MoveContainerContents("AR0322*container5","#LBG0006*PlayerChest00") MoveContainerContents("AR0411*#LPlayerChest","#LBG0006*PlayerChest00") MoveContainerContents("AR1902*#LPlayerChest","#LBG0006*PlayerChest00") MoveContainerContents("AR0901*#LPlayerChest","#LBG0006*PlayerChest00") MoveContainerContents("AR0902*#LPlayerChest","#LBG0006*PlayerChest00") MoveContainerContents("AR0903*#LPlayerChest","#LBG0006*PlayerChest00") MoveContainerContents("AR0904*#LPlayerChest","#LBG0006*PlayerChest00") MoveContainerContents("AR4500*#LPlayerChest","#LBG0006*PlayerChest00")~
		END
	END
	COPY_EXISTING ~#LPCRoom.BCS~ ~override~
		// Update the script used on all PC residences to include the one in front of the ducal palace
	   DECOMPILE_AND_PATCH BEGIN 
			// Move the contents from this house's chest to the ducal palace chest when they go there
			REPLACE_TEXTUALLY ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1)~ ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1) MoveContainerContents("#LBG0006*PlayerChest00","#LBD0103*PlayerChest00")~

			// Add the block where the ducal palace and army camp chest contents get move to the house
			APPEND_FILE TEXT ~%mod_root%/scripts/#LPCRooma.baf~ EVAL
		END
	BUT_ONLY_IF_IT_CHANGES

	// Prep PC's house in front of the Ducal Palace
	EXTEND_BOTTOM ~%NBaldursGate%.BCS~ ~%mod_root%/scripts/BG0200c.baf~ EVAL
	LAF UPDATE_BCS
		STR_VAR
			baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
			baf_name = ~SoDPalaceExterior~			// like my_new_BG0146
			bcs_name = ~BD0010~			// like BG0146
	END
	LAF UPDATE_BCS
		STR_VAR
			baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
			baf_name = ~SoDPalaceExterior~			// like my_new_BG0146
			bcs_name = ~BD0101~			// like BG0146
	END
	
	// If PC ever meets Rinnie in the original BG0006, set her up as
	// a global so we can try to move her if the PC picks her house
	// as their residence
	LAF UPDATE_BCS
		STR_VAR
			baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
			baf_name = ~BG0006~			// like my_new_BG0146
			bcs_name = EVAL ~%NBaldursGate_RinniesHouse_L1%~			// like BG0146
	END

	// Update the dialogue of the servant who welcomes them
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/house0_servant.d~ USING ~%tra_loc%/%s/dialogues.tra~

	// Set the global to indicate that the house in north Baldur's Gate is available as a reward
	EXTEND_TOP ~%NBaldursGate_DucalPalace_L1%.BCS~ ~%mod_root%/scripts/bg0108f.baf~
END