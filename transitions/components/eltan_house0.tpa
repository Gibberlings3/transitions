//////////////////////////////////////////////////////////////////////////////
// Optional component that adds the house in north BG near the Ducal Palace //
// to the list of properties available as a reward from Duke Eltan          //
// Item strings are in game.tra                                             //
//////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION eltan_house0 BEGIN
	INCLUDE ~%mod_root%/components/eltan_common.tpa~

	// Add the key he gives to the protagonist for their new house in front of the Ducal Palace
	COPY_EXISTING ~TTKey01.itm~ ~override/#LKey01.itm~
		SAY NAME1 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY NAME2 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY UNIDENTIFIED_DESC @1010
		SAY IDENTIFIED_DESC @1010
		WRITE_SHORT (0x0018) 44 // Flags
		WRITE_SHORT (0x0034) 10000 // Price
		WRITE_ASCII (0x003a) ~IMISC93~
		WRITE_ASCII (0x0058) ~GMISC93~

	COPY_EXISTING ~%NBaldursGate%.ARE~ ~override~
		// Change the name of the region leading into BG0006
		// or else we can't control the door object because it has the same name
		LPF CHANGE_DOOR_NAME
			STR_VAR
				old_name = ~Door0006~
				new_name = ~Port0006~
		END

		// Clone the travel trigger leading into the house so we can 
		// have the door lead to a custom area
		LPF GW_CLONE_TRAVEL_TRIGGER
			INT_VAR
				GW_Flags_new = 256 // Deactivated
			STR_VAR
				GW_Name_old = ~Door0006~
				GW_Name_new = ~Door#lbg0006~
				GW_Dest_new = ~#LBG0006~
				GW_Key_new = ~~      // Clear out the key if one exists
				GW_Script_new = ~~   // Clear out the script if one exists
		END

		// Need to modify the door object in BG0200 leading to the house in front 
		// of the palace in case that one is chosen as their reward
		LPF ALTER_AREA_DOOR
			INT_VAR
				lock_difficulty = 100
			STR_VAR
				door_name = ~Port0006~
				door_open_sound = ~AMB_D22B~
				door_key = ~#LKey01~
		END
	BUT_ONLY_IF_IT_CHANGES

	// Also need to add a new area that can be attached to the door
	// if/when it is chosen as a reward
	COPY ~%mod_root%/areas/#LBG0006.ARE~ ~override~
	COPY ~%mod_root%/areas/#LBG0007.ARE~ ~override~

	// Update the script used on all PC residences to include the one in front of the ducal palace
	COPY_EXISTING ~#LPCRoom.BCS~ ~override~
	   DECOMPILE_AND_PATCH BEGIN 
			// Move the contents from this house's chest to the ducal palace chest when they go there
			REPLACE_TEXTUALLY ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1)~ ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1) MoveContainerContents("#LBG0006*PlayerChest00","#LBD0103*PlayerChest00")~

			// Add the block where the ducal palace chest contents get move to the house
			APPEND_FILE TEXT ~%mod_root%/scripts/#LPCRooma.baf~
		END
	BUT_ONLY_IF_IT_CHANGES

	// Prep PC's house in front of the Ducal Palace
	EXTEND_BOTTOM ~%NBaldursGate%.BCS~ ~%mod_root%/scripts/BG0200c.baf~

	// If PC ever meets Rinnie in the original BG0006, set her up as
	// a global so we can try to move her if the PC picks her house
	// as their residence
	LAF UPDATE_BCS
		STR_VAR
			baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
			baf_name = ~BG0006~			// like my_new_BG0146
			bcs_name = EVAL ~%NBaldursGate_RinniesHouse_L1%~			// like BG0146
	END

	// Update the dialogue of the servant who welcomes them
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/house0_servant.d~ USING ~%tra_loc%/%s/dialogues.tra~

	// Set the global to indicate that the house in north Baldur's Gate is available as a reward
	EXTEND_TOP ~%NBaldursGate_DucalPalace_L1%.BCS~ ~%mod_root%/scripts/bg0108f.baf~
END