////////////////////////////////////////////////////////////////////////
// Optional component that removes the wounded from the thieves' maze //
// after Sarevok has been defeated                                    //
////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION cleanup_maze BEGIN
	// Fix folks if Endless BG1 'sarevok_cleanup...' components are loaded
	ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ 0) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL "%mod_root%.tp2" "C#-EBG1-BG_CHATTER")) THEN BEGIN
		INCLUDE ~%mod_root%/components/fix_maze.tpa~
	END

	// First deal with Winski since we already have a component set up for doing just that
	ACTION_IF NOT ((MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL "%mod_root%.tp2" "#L-TRANSITIONS-QUESTS_FLEE_HARSH")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL "%mod_root%.tp2" "#L-TRANSITIONS-QUESTS_NO_FLEE_HARSH")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL "%mod_root%.tp2" "#L-TRANSITIONS-QUESTS_FLEE_GENTLE")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL "%mod_root%.tp2" "#L-TRANSITIONS-QUESTS_NO_FLEE_GENTLE"))) THEN BEGIN
		// Add in code that sets up Winski to escape
		INCLUDE ~%mod_root%/components/winski_escapes.tpa~
	END

	// Need to give Voleta in the thieves' maze a variable name or else we can't clear her out of the maze
	COPY_EXISTING ~VOLETA.cre~ ~override~
	    WRITE_ASCII 0x280 ~VOLETA~ #32 // set DV

	// Add Voleta's block to maze script so she poofs as well
	EXTEND_BOTTOM ~%ThievesMaze%.BCS~ ~%mod_root%/scripts/BG0146b.baf~

	// Append to the trigger that spawns Denkod so he doesn't spawn right after we have him leave
	// This might already be done via jastey's Endless BG1 main component
	// But duplicating the effort here makes no difference
	LAF ALTER_SCRIPT_BLOCK
		INT_VAR
			only_once = 1
		STR_VAR
			script = EVAL %EBaldursGate_ThievesGuild%
			match = ~Global("DukeThanks","GLOBAL",1)~
			match1 = ~!Exists("DENKOD")~
			match2 = ~!Dead("DENKOD")~
			match3 = ~!Dead("blacklily")~
			swap_out = ~!Dead("blacklily")~
			swap_in = ~!Dead("blacklily") !Dead("Sarevok")~
	END

	// Set up the thieves' guild area script to start dialogue for Denkod
	// so he can explain what happened to the wounded that were in the thieves' maze
	EXTEND_TOP ~%EBaldursGate_ThievesGuild%.BCS~ ~%mod_root%/scripts/BG0153a.baf~

	// Add to Denkod's dialogue file
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/denkod.d~ USING ~%tra_loc%/%s/dialogues.tra~
END
