/////////////////////////////////////////////////////////
// Actions common to all options of the main component //
// Included via main0-2.tpa but only once              //
/////////////////////////////////////////////////////////

ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ 0) BEGIN
   // Remove remnant of block left behind by jastey
   LAF DELETE_SCRIPT_BLOCK
      INT_VAR
         only_once = 0
      STR_VAR
         script = BG0125
         match = ~StartCutSceneEx("BDSODTRN",TRUE)~
   END
   // Remove jastey's code to prep the dukes in the palace - we do it differently
   LAF DELETE_SCRIPT_BLOCK
      INT_VAR
         only_once = 0
      STR_VAR
         script = BG0108
         match = ~C#st_RearrangeDukes~
   END
   // Remove jastey's guards and opening of the palace
   LAF DELETE_SCRIPT_BLOCK
      INT_VAR
         only_once = 0
      STR_VAR
         script = BG0200
         match = ~C#st_SarevokDeadGuards~
   END
   LAF DELETE_SCRIPT_BLOCK
      INT_VAR
         only_once = 0
      STR_VAR
         script = BG0200
         match = ~C#st_SarevokDeadOpenGate~
   END
END

// New line in campaign.2da so we can use the right post-dialogue files
APPEND ~CAMPAIGN.2DA~ ~#LSOD1         BDBALDUR       -1             1              BDBANTER       *              MASTAREA       152            -1             *              BDPARTY        BDDIALOG       save           #LSTRTA1       STRTGOLD       *              SODWEAP        BDSTWEAP       *              *              BGMAP          BDBALDUR       1              0xFFFFFFFF     1              *              PARTY          SODINTER       SODYEARS       REPUTATI       SODCLTXT       SODRACE~

APPEND ~CAMPAIGN.2DA~ ~#LSOD2         BDBALDUR       -1             1              BDBANTER       *              MASTAREA       152            -1             *              BDPARTY        BDDIALOG       save           #LSTRTA2       STRTGOLD       *              SODWEAP        BDSTWEAP       *              *              BGMAP          BDBALDUR       1              0xFFFFFFFF     1              *              PARTY          SODINTER       SODYEARS       REPUTATI       SODCLTXT       SODRACE~

// New starting areas
COPY ~%mod_root%/objects/#LSTRTA1.2DA~ ~override~
COPY ~%mod_root%/objects/#LSTRTA2.2DA~ ~override~

// 3 Flaming Fist escort ... used in various places
COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc1.cre~
   SAY NAME1 @1007 /* ~Flaming Fist Escort~ */
   SAY NAME2 @1007 /* ~Flaming Fist Escort~ */
   WRITE_ASCII 0x280 ~#LffEsc1~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
   WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
   WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc2.cre~
   SAY NAME1 @1008 // ~Flaming Fist Escort~
   SAY NAME2 @1008 // ~Flaming Fist Escort~
   WRITE_ASCII 0x280 ~#LffEsc2~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
   WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
   WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc3.cre~
   SAY NAME1 @1008 // ~Flaming Fist Escort~
   SAY NAME2 @1008 // ~Flaming Fist Escort~
   WRITE_ASCII 0x280 ~#LffEsc3~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
   WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
   WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

// Dialogues for the Flaming Fist escort that are used for various components
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/ffescort.d~ USING ~%tra_loc%/%s/dialogues.tra~

// New general script file for new flaming fist healers et al
// Stops them from wandering around when first spawned
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LffGen.baf~

// Palace personnel dialogues
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/palace_personnel.d~ USING ~%tra_loc%/%s/dialogues.tra~

// New general script file for new palace servant
// Stops it from wandering around when first spawned
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LSrvGen.baf~

// Palace gate guards
COPY_EXISTING ~flambrid.cre~ ~override/#LDPGrd1.cre~
   WRITE_ASCII 0x280 ~#LDPGrd1~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LDPGrd1~ #8 // set dialogue file

COPY_EXISTING ~flambrid.cre~ ~override/#LDPGrd2.cre~
   WRITE_ASCII 0x280 ~#LDPGrd2~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LDPGrd1~ #8 // set dialogue file

// Palace servant on the 3rd floor
COPY_EXISTING ~servan.cre~ ~override/#LDPSrv1.cre~
   WRITE_ASCII 0x280 ~#LDPSrv1~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LDPSrv1~ #8 // set dialogue file
   WRITE_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander

// Open the palace gates
// And place friendly guards
COPY_EXISTING ~BG0200.BCS~ ~override~
   DECOMPILE_AND_PATCH BEGIN 
      APPEND_FILE TEXT ~%mod_root%/scripts/BG0200a.baf~
   END
BUT_ONLY_IF_IT_CHANGES

// Place the servant in the custom SoD version of the third floor
// This is a common script used in all PC personal quarters
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LPCRoom.baf~

// Modified copy of SoD's version of the third floor
COPY ~%mod_root%/areas/#LBD0103.ARE~ ~override~ 

// Open the way to the PCs rooms
LAF UPDATE_BCS
   STR_VAR
      baf_location = EVAL ~%mod_root%/scripts/~
      baf_name = ~BG0109~
      bcs_name = ~BG0109~
END

// Add 2 new inactive exit travel triggers leading from the 2nd floor to the new 3rd floor
// Activate the new ones and deactive the original ones when/if the PC and/or Imoen take up residence
COPY_EXISTING ~BG0109.ARE~ ~override~
   LPF GW_CLONE_TRAVEL_TRIGGER
      INT_VAR
         GW_Flags_new = 256 // Deactivated
      STR_VAR
         GW_Name_old = ~Door0110a~
         GW_Name_new = ~Door#lbd0103a~
         GW_Dest_new = ~#LBD0103~
         GW_Entre_new = ~ExitBG0109a~
         GW_Key_new = ~~      // Clear out the key if one exists
         GW_Script_new = ~~   // Clear out the script if one exists
   END

   LPF GW_CLONE_TRAVEL_TRIGGER
      INT_VAR
         GW_Flags_new = 256 // Deactivated
      STR_VAR
         GW_Name_old = ~Door0110b~
         GW_Name_new = ~Door#lbd0103b~
         GW_Dest_new = ~#LBD0103~
         GW_Entre_new = ~ExitBG0109b~
         GW_Key_new = ~~      // Clear out the key if one exists
         GW_Script_new = ~~   // Clear out the script if one exists
   END
BUT_ONLY_IF_IT_CHANGES

// Update Duke Belt's dialogue file
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/duke_belt.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Update Duke Liia's dialogue file
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/duke_liia.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Update Imoen's dialogue file and script to initiate pre-sod exit dialogue
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/imoen_training.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~IMOEN_.BCS~ ~%mod_root%/scripts/imoen.baf~

// Update Duke Belt's override script so he'll initiate conversation when PC first arrives in palace
EXTEND_TOP ~BELT.BCS~ ~%mod_root%/scripts/Belt_Add.baf~

// Make sure Duke Belt and Liia are alive and in position inside the Ducal Palace
// Also sets up dialogue cues
COPY_EXISTING ~BG0108.BCS~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      APPEND_FILE TEXT ~%mod_root%/scripts/BG0108a.baf~
   END
BUT_ONLY_IF_IT_CHANGES

// Add key rings to Black Lily, Silence, Sorcery Sundries (both BG1 and SoD versions), 
// the quartermaster in SoD and Ribald's in BG2 just for good measure
COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR01.ITM~ 
   SAY NAME1 @1014 /* ~Black Key Ring~ */
   SAY NAME2 @1014 /* ~Black Key Ring~ */
   SAY UNIDENTIFIED_DESC @1020
   SAY IDENTIFIED_DESC @1020
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR01.STO~ 
   SAY 0x0c @1014
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~STOBLACK.sto~ ~override~   // Black Lily
   ADD_STORE_ITEM ~#LKEYR01~ AFTER ~SLNG11~ #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR02.ITM~ 
   SAY NAME1 @1015 /* ~Silver Key Ring~ */
   SAY NAME2 @1015 /* ~Silver Key Ring~ */
   SAY UNIDENTIFIED_DESC @1021
   SAY IDENTIFIED_DESC @1021
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR02.STO~ 
   SAY 0x0c @1015
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~STO0703.STO~ ~override~  // BG Sorcerer's Sundries
   ADD_STORE_ITEM ~#LKEYR02~ LAST #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR03.ITM~ 
   SAY NAME1 @1016 /* ~Brass Key Ring~ */
   SAY NAME2 @1016 /* ~Brass Key Ring~ */
   SAY UNIDENTIFIED_DESC @1022
   SAY IDENTIFIED_DESC @1022
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR03.STO~ 
   SAY 0x0c @1016
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKAZZRE.STO~ ~override~  // Kazzrem inside BD Sorcerer's Sundries
   ADD_STORE_ITEM ~#LKEYR03~ LAST #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR04.ITM~ 
   SAY NAME1 @1017 /* ~Steel Key Ring~ */
   SAY NAME2 @1017 /* ~Steel Key Ring~ */
   // Current description already says steel
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR04.STO~ 
   SAY 0x0c @1017
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDBELEG4.sto~ ~override~   // Quartermaster Belegarm
   ADD_STORE_ITEM ~#LKEYR04~ AFTER ~BAG31~ #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR05.ITM~ 
   SAY NAME1 @1018 /* ~Iron Key Ring~ */
   SAY NAME2 @1018 /* ~Iron Key Ring~ */
   SAY UNIDENTIFIED_DESC @1023
   SAY IDENTIFIED_DESC @1023
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR05.STO~ 
   SAY 0x0c @1018
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~STOSILEN.sto~ ~override~   // Silence
   ADD_STORE_ITEM ~#LKEYR05~ LAST #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR06.ITM~ 
   SAY NAME1 @1019 /* ~Copper Key Ring~ */
   SAY NAME2 @1019 /* ~Copper Key Ring~ */
   SAY UNIDENTIFIED_DESC @1024
   SAY IDENTIFIED_DESC @1024
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR06.STO~ 
   SAY 0x0c @1019
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~RIBALD.sto~ ~override~   // Ribald's Adventurer Mart
   ADD_STORE_ITEM ~#LKEYR06~ AFTER ~BAG02~ #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

// New cut scene called from Duke Belt's dialogue for Korlasz quest
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Cut04.baf~

// Update BDRope.bcs so that when someone clicks on it, it'll take the party to the surface
// instead of starting the ending dialogue
COPY_EXISTING ~BDRope.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Clicked([ANYONE])~ ~Clicked([ANYONE]) !Global("#L_SarvQuests","GLOBAL",8)~
   END
BUT_ONLY_IF_IT_CHANGES
EXTEND_TOP ~BDRope.bcs~ ~%mod_root%/scripts/BDRope.baf~

// Update 1st Korlasz Safehouse area script so no group or equipment swaps
COPY_EXISTING ~BD0120.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("K#NewGame","BD0120",0)~ ~Global("K#NewGame","BD0120",0) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("K#StoryMode","BD0120",0)~ ~Global("K#StoryMode","BD0120",0) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~GlobalLT("bd_plot","global",2)~ ~GlobalLT("bd_plot","global",2) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~!InMyArea("IMOEN2")~ ~!InMyArea("IMOEN2") !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~TriggerOverride("Imoen_Room1b",Detect([PC]))~ ~TriggerOverride("Imoen_Room1b",Detect([PC])) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~TriggerOverride("Imoen_room1a",Detect([PC]))~ ~TriggerOverride("Imoen_room1a",Detect([PC])) !Global("#L_SarvQuests","GLOBAL",8)~
   END
BUT_ONLY_IF_IT_CHANGES

// Use a modified intro scene and other modified blocks that moved Imoen about
LAF INSERT_SCRIPT_BLOCK
   INT_VAR 
      insert_above = 0
      only_once = 1
   STR_VAR 
      script = BD0120
      match = ~GlobalLT("bd_plot","global",2)~
      match1 = ~StartCutSceneEx("bdintro",TRUE)~
      insert = ~%mod_root%/scripts/bd0120.baf~
END
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Cut05.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Skip.baf~

// Updates to 2nd Korlasz Safehouse area script to sidestep ending quest hand-holding
COPY_EXISTING ~BD0130.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~!Global("BD_Korlasz_Fight","BD0130",2)~ ~!Global("BD_Korlasz_Fight","BD0130",2) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~!TriggerOverride("Bhaal_Mural",Detect([PC]))~ ~!TriggerOverride("Bhaal_Mural",Detect([PC])) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~!TriggerOverride("Dead_Three_Center",Detect([PC]))~ ~!TriggerOverride("Dead_Three_Center",Detect([PC])) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~!TriggerOverride("Myrkul_mural",Detect([PC]))~ ~!TriggerOverride("Myrkul_mural",Detect([PC])) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~!Global("BDSH_Imoen_Room","BD0130",6)~ ~!Global("BDSH_Imoen_Room","BD0130",6) !Global("#L_SarvQuests","GLOBAL",8)~
   END
BUT_ONLY_IF_IT_CHANGES
// Replacement blocks for the ones side-stepped above
LAF INSERT_SCRIPT_BLOCK
   INT_VAR 
      insert_above = 0
      only_once = 1
   STR_VAR 
      script = BD0130
      match = ~!Global("BD_Korlasz_Fight","BD0130",2)~
      insert = ~%mod_root%/scripts/bd0130.baf~
END

// Update to cut scene when entering BG0130 for the first time that spawns the FF escort
// Make sure Imoen isn't also spawned
COPY_EXISTING ~BDSHCUT0.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("BDSH_Spawn_Imoen","GLOBAL",0)~ ~Global("BDSH_Spawn_Imoen","GLOBAL",0) !Global("#L_SarvQuests","GLOBAL",8)~
   END
BUT_ONLY_IF_IT_CHANGES

// Modified dialogues for Flaming Fist escorts in Korlasz Dungeon
// Removes calls to Imoen's dialogue if doing quest
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/bdff100x.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Modified version of Korlasz's Journal with no mention of refugees
COPY_EXISTING ~BDSHKORJ.ITM~ ~override~
   SAY UNIDENTIFIED_DESC @1027
   SAY IDENTIFIED_DESC @1027
BUT_ONLY_IF_IT_CHANGES

// Add an info area where the group exits from Korlasz's family crypt
// Can't say I'm completely happy with this, but it's better than nothing
COPY_EXISTING ~BG0200.ARE~ ~override~
   LPF ADD_AREA_REGION_TRIGGER
      INT_VAR
         ab_RT_Type = 1
         ab_RT_BbLX = 1356
         ab_RT_BbLY = 532
         ab_RT_BbHX = 1558
         ab_RT_BbHY = 619
         ab_RT_VxPr = 4
         ab_RT_Vx_X_0 = 1356
         ab_RT_Vx_Y_0 = 532
         ab_RT_Vx_X_1 = 1368
         ab_RT_Vx_Y_1 = 618
         ab_RT_Vx_X_2 = 1558
         ab_RT_Vx_Y_2 = 619
         ab_RT_Vx_X_3 = 1540
         ab_RT_Vx_Y_3 = 545
         ab_RT_Itxt = RESOLVE_STR_REF (@1028) /* ~Sealed exit from the Korlasz family crypt~ */
      STR_VAR
         ab_RT_Name = "#L_KorlaszExit"
   END
BUT_ONLY_IF_IT_CHANGES

// Put the exit dialogue stuff into its own file because it was pretty huge
INCLUDE ~%mod_root%/components/exit_dialogues.tpa~
LAF exit_dialogues END

// The 'magic' item given by Duke Belt that will allow for game advancement
//   and getting to the SoD version of the palace
// PC will find unusable broken remnants of it in Chateau Irenicus
//   if it's in their inventory at the time of capture
// PC will find a 'drained' unusable version of it in their personal chest
//   if it is transfered to their stronghold in BG2
COPY_EXISTING ~COMPON02.ITM~ ~override/#LAdvGm1.itm~ // I like the pretty icon
   SAY NAME1 @1031 /* ~Duke Belt's Gift to <CHARNAME>~ */
   SAY NAME2 @1031 /* ~Duke Belt's Gift to <CHARNAME>~ */
   SAY UNIDENTIFIED_DESC @1032 /* ~This item is able to grant you a limited number of specific wishes.~ */
   SAY IDENTIFIED_DESC @1032 /* ~This item is able to grant you a limited number of specific wishes.~ */
   WRITE_LONG (0x0018) 2093 // Critical Item, Dropable, Displayable, Not Copyable, Conversable
   WRITE_LONG (0x0034) 0 // Price-Discourage it's sale though Critical Item should stop it from being sold
   WRITE_SHORT (0x0042) 0 // Doesn't need a bard to identify

// The dialogue for the item
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/#LAdvGm1.d~ USING ~%tra_loc%/%s/dialogues.tra~

OUTER_SET UseMe = RESOLVE_STR_REF (@1033)
APPEND itemdial.2da ~#LAdvGm1 %UseMe% #LAdvGm1~ UNLESS ~#LAdvGm1~

// Timer put on the PC so it'll transition to SoD or BG2 when solo
EXTEND_TOP ~DPLAYER3.BCS~ ~%mod_root%/scripts/solo_check.baf~

// Cut scene that starts SoD
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Cut07.baf~

// Cut scene that starts BG2
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Cut09.baf~

// Updated Start SoD scrolling text
COPY ~%mod_root%/objects/#LSoD01.2DA~ ~override~
   REPLACE "REPLACEME" @1042

// Allow for only 2nd half to be run
COPY_EXISTING ~BD0103.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("BD_PARTY_ITEMS","BD0103",0)~ ~Global("BD_PARTY_ITEMS","BD0103",0) !Global("#L_SkipFirstHalfCA","GLOBAL",1)~
      // Using the cutspy to drop Imoen's wand - its demise is dealt with later
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ActionOverride("cutspy",DestroySelf())~ ~~
   END
BUT_ONLY_IF_IT_CHANGES

// Put in the modified Caelar Elite attack scene and the new demise of cutspy so it'll drop Imoen's wand
LAF INSERT_SCRIPT_BLOCK
   INT_VAR 
      insert_above = 1
      only_once = 1
   STR_VAR 
      script = BD0103
      match = ~TextScreen("DPALACE")~
      insert = ~%mod_root%/scripts/bd0103.baf~
END

// SoD Palace dialogue
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/sod_palace.d~ USING ~%tra_loc%/%s/dialogues.tra~

// For now, just get rid of Ophyllis if PC didn't lose their gold
EXTEND_TOP ~BD0116.BCS~ ~%mod_root%/scripts/BD0116_skip_treasurer.baf~

// A wanted poster if the PC wasn't allowed to leave Baldur's Gate by the Dukes
// A few of them will eventually be dispersed around Athkatla
// First will be for PC to find in Chateau Irenicus
// Future version: Will give some to folks in BG1 areas as well if the PC shows up there
COPY ~%mod_root%/objects/#LScr01.itm~ ~override~
    SAY NAME2 @1038 // ~Warrant for <CHARNAME>'s Arrest~
    SAY DESC  @1039

// Broken gift from Duke Belt
COPY ~%mod_root%/objects/#LMsc01.itm~ ~override~
    SAY NAME2 @1040 // ~Broken Gift from Duke Belt~
    SAY DESC  @1041

// Save some custom Transitions items in Chateau Irenicus
EXTEND_TOP ~K#TELBGT.bcs~ ~%mod_root%/scripts/k#TelBGT.baf~
EXTEND_TOP ~AR0602.bcs~ ~%mod_root%/scripts/ar0602.baf~

// Skip the intro movie if using my transition
// We've just lived it, we don't need a recap
COPY_EXISTING ~K#TELBGT.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~True()~ ~Global("#L_StartBG2","GLOBAL",0)~
   END
BUT_ONLY_IF_IT_CHANGES
EXTEND_BOTTOM ~K#TELBGT.bcs~ ~%mod_root%/scripts/k#TelBGT_end.baf~

// Suldanessellar Palace dialogue to allow for continuation of BG2
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/elf_palace.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Item that Queen Essemine gives to PC that will advance the game to ToB
// This is just a slightly modified version of her normal award
// This version get destroyed upon use and the normal award given at the start of ToB
COPY_EXISTING ~AMUL27.ITM~ ~override/#LAMUL27.itm~
   WRITE_LONG (0x0018) 2157 // Critical Item, Dropable, Displayable, Not Copyable, Magical, Conversable

APPEND itemdial.2da ~#LAMUL27 %UseMe% #LAMUL27~ UNLESS ~#LAMUL27~

