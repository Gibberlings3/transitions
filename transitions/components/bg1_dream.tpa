//////////////////////////////////////////////////////////////////////////////
// Component that allows uses the BG1 end movie as part of a dream sequence //
// that gives the protagonist an additional bhaal-spawn power               //
//////////////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION bg1_dream BEGIN
	// Build a new spell for the good guys (and neutrals)
	COPY ~%mod_root%/objects/#LPWR01B.BAM~ ~override~	// Blue version of spwi506B
	COPY ~%mod_root%/objects/#LPWR01C.BAM~ ~override~	// Blue version of spwi506c

	COPY ~%mod_root%/objects/BLANK_INNATE.SPL~ ~override\#LPWR01.SPL~
		WRITE_LONG 0x0008 23748	// Mind Shield strref in all campaigns
		SAY 0x0050 @1050	// Mind shield description (modified version of Greenstone Amulet description)
		WRITE_ASCII 0x003a ~#LPWR01C~
		LPF ALTER_SPELL_HEADER
			INT_VAR
				header = 1
				target = 5 // Caster
			STR_VAR
				icon = ~#LPWR01B~
		END
		
	COPY_EXISTING ~AMUL17.ITM~ ~override~
		LPF ITEM_EFFECT_TO_SPELL
			INT_VAR
				type = 99
				header = 99
				insert_point = 0
			STR_VAR
				new_itm_spl = ~#LPWR01.SPL~
		END
	BUT_ONLY_IF_IT_CHANGES
	
	// Build a new spell for the not-so good guys
	COPY ~%mod_root%/objects/BLANK_INNATE.SPL~ ~override\#LPWR02.SPL~
		SAY 0x0008 @1051	// Charm Creatue
		SAY 0x0050 @1052	// Charm Creature description (modified version of Algernon's Cloak description)
		WRITE_ASCII 0x003a ~SPWI104C~
		LPF ALTER_SPELL_HEADER
			INT_VAR
				header = 1
				target = 1 // Living Target
				range = 60
			STR_VAR
				icon = ~SPWI104B~
		END
		
	COPY_EXISTING ~CLCK08.ITM~ ~override~
		LPF ITEM_EFFECT_TO_SPELL
			INT_VAR
				type = 99
				header = 99
				insert_point = 0
			STR_VAR
				new_itm_spl = ~#LPWR02.SPL~
		END
	BUT_ONLY_IF_IT_CHANGES

	// New dream and bhaal-spawn power
	OUTER_SET STRREF_DRMTXT = RESOLVE_STR_REF(@1053)
	COPY ~%mod_root%/objects/#LDRM1.WAV~ ~override~
	COPY ~%mod_root%/objects/#LDRM1.2DA~ ~override~
		REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~DREAM_TEXT~ ~%STRREF_DRMTXT%~
	COPY ~%mod_root%/objects/#LDRM1.MOS~ ~override~
	EXTEND_BOTTOM ~PLAYER1D.BCS~ ~%mod_root%/scripts/newdrm1.baf~ EVAL
	
	// Group discussion on the dream
	EXTEND_BOTTOM ~BALDUR.BCS~ ~%mod_root%/scripts/newdrm1_talk.baf~ EVAL
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/#LGroup.d~ EVAL USING ~%tra_loc%/%s/dialogues.tra~		

	// Stop the end-movie from happening when Sarevok is defeated
    COPY_EXISTING ~%Undercity_TempleofBhaal%.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~StartMovie("ENDMOVIE")~ ~~ 
		END
END

