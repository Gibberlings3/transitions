///////////////////////////////////////////////////////////////////
// Optional component that adds the 3 story house in Beregost to //
// the list of properties available as a reward from Duke Eltan  //
// Item strings are in game.tra                                  //
///////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION eltan_house2 BEGIN
	ACTION_IF !((MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_BG_N")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_HEDGE")) OR (MOD_IS_INSTALLED ~%mod_root%.tp2~ (ID_OF_LABEL ~%mod_root%.tp2~ "#L-TRANSITIONS-ELTAN_HOUSE_BG_SW"))) BEGIN
		INCLUDE ~%mod_root%/components/eltan_common.tpa~
	END

	// Add the key he gives to the protagonist for their new house in Beregost
	COPY_EXISTING ~TTKey01.itm~ ~override/#LKey03.itm~
		SAY NAME1 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY NAME2 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY UNIDENTIFIED_DESC @1012
		SAY IDENTIFIED_DESC @1012
		WRITE_SHORT (0x0018) 44 // Flags
		WRITE_SHORT (0x0034) 10000 // Price
		WRITE_ASCII (0x003a) ~IMISC93~
		WRITE_ASCII (0x0058) ~GMISC93~

	COPY_EXISTING ~%Beregost%.ARE~ ~override~
		// Change the name of the door leading into BG3347
		// or else we can't control it because it has the same name 
		// as the travel trigger region
		LPF CHANGE_DOOR_NAME
			STR_VAR
				old_name = ~Door3347~
				new_name = ~Port3347~
		END

		// Clone the travel trigger leading into the house so we can 
		// have the door lead to a custom area
		LPF GW_CLONE_TRAVEL_TRIGGER
			INT_VAR
				GW_Flags_new = 256 // Deactivated
			STR_VAR
				GW_Name_old = ~Door3347~
				GW_Name_new = ~Door#lbg3347~
				GW_Dest_new = ~#LBG3347~
				GW_Key_new = ~~      // Clear out the key if one exists
				GW_Script_new = ~~   // Clear out the script if one exists
		END

		// Need to modify the door object in BG3300 leading into the house 
		// in case that one is chosen as their reward
		LPF ALTER_AREA_DOOR
			INT_VAR
				lock_difficulty = 100
			STR_VAR
				door_name = ~Port3347~
				door_open_sound = ~AMB_D21D~
				door_key = ~#LKey03~
		END
	BUT_ONLY_IF_IT_CHANGES

	// Also need to add a new area that can be attached to the door
	// if/when it is chosen as a reward
	COPY_EXISTING ~%NWBaldursGate_House3_L1%.are~ ~override/#LBG3347.are~
		WRITE_ASCII (AREA_SCRIPT) ~#LPCRoom~
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Male Townsfolk~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF CHANGE_CONTAINER_NAME STR_VAR old_name = ~Container 1~ new_name = ~PlayerChest00~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0100~ new_name = ~Door3300~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0156~ new_name = ~Door3348~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0100~ new_name = ~Exit3300~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0156~ new_name = ~Exit3348~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door3348~ destination_area = ~#LBG3348~ entrance_name = ~Exit3347~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door3300~ destination_area = EVAL ~%Beregost%~ entrance_name = ~Exit3347~ END
	COPY_EXISTING ~%NWBaldursGate_House2_L2%.are~ ~override/#LBG3348.are~
		WRITE_ASCII (AREA_SCRIPT) ~~
		WRITE_LONG (0x0048) 128 // Allow sleeping
		LPF REMOVE_ACTOR STR_VAR actor_name = ~Sleeping Man~ END
		LPF UNLOCK_ALL_CONTAINERS END
		LPF DELETE_AREA_ITEM STR_VAR item_to_delete = ~.+~ END
		LPF CHANGE_REGION_NAME STR_VAR old_name = ~Door0163~ new_name = ~Door#LBG3347~ END
		LPF CHANGE_ENTRANCE_NAME STR_VAR old_name = ~Exit0163~ new_name = ~Exit3347~ END
		LPF ALTER_AREA_REGION STR_VAR region_name = ~Door#LBG3347~ destination_area = ~#LBG3347~ entrance_name = ~Exit3348~ END

	// Update the script used on all PC residences to include the one in front of the ducal palace
	OUTER_SPRINT SOD_CHEST_TRANS ~~
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		OUTER_SPRINT SOD_CHEST_TRANS ~MoveContainerContents("BD0103*PlayerChest00","#LBG3347*PlayerChest00") MoveContainerContents("BD1000*PlayerChest00","#LBG3347*PlayerChest00") MoveContainerContents("BD7100*PlayerChest00","#LBG3347*PlayerChest00") MoveContainerContents("BD3000*PlayerChest00","#LBG3347*PlayerChest00")~
	END
	COPY_EXISTING ~#LPCRoom.BCS~ ~override~
	   DECOMPILE_AND_PATCH BEGIN 
			// Move the contents from this house's chest to the ducal palace chest when they go there
			REPLACE_TEXTUALLY ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1)~ ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1) MoveContainerContents("#LBG3347*PlayerChest00","#LBD0103*PlayerChest00")~

			// Add the block where the ducal palace chest contents get move to the house
			APPEND_FILE TEXT ~%mod_root%/scripts/#LPCRoomc.baf~ EVAL
		END
	BUT_ONLY_IF_IT_CHANGES

	// Prep PC's house in Beregost
	LAF UPDATE_BCS
		STR_VAR 
			baf_location = EVAL ~%mod_root%/scripts/~
			baf_name = ~BG3300~
			bcs_name = EVAL ~%Beregost%~
	END

	// Update the dialogue of the servant who welcomes them
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/house2_servant.d~ USING ~%tra_loc%/%s/dialogues.tra~

	// Set the global to indicate that the cabin in Beregost is available as a reward
	EXTEND_TOP ~%NBaldursGate_DucalPalace_L1%.BCS~ ~%mod_root%/scripts/BG0108e.baf~
END
