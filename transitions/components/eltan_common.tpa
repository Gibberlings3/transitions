///////////////////////////////////////////////////////////////////////////////////
// Common elements to the component that adds in Duke Eltan's reward and romance //
// Included via eltan_house0-3 tpa files                                         //
// Item strings are in game.tra                                                  //
///////////////////////////////////////////////////////////////////////////////////

// Remove jastey's code to prep the dukes in the palace - we do it differently
LAF DELETE_SCRIPT_BLOCK
	INT_VAR
		only_once = 0
	STR_VAR
		script = EVAL ~%NBaldursGate_DucalPalace_L1%~
		match = ~C#st_SpawnEltan~
END

// Make sure Eltan is alive and in position in the Ducal Palace
// if the protagonist delivered him safely to the harbor master
EXTEND_TOP ~%NBaldursGate_DucalPalace_L1%.BCS~ ~%mod_root%/scripts/BG0108c.baf~

// Modify his dialogue file
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/duke_eltan.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Make sure he's no longer with the Harbor Master
LAF UPDATE_BCS
	STR_VAR
		baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
		baf_name = ~BG0702~			// like my_new_BG0146
		bcs_name = EVAL ~%BaldursGateDocks_Harbormaster%~			// like BG0146
END

// Update SoD's handling of the chest
ACTION_IF GAME_INCLUDES ~sod~ BEGIN
	COPY_EXISTING ~BD1000.BCS~ ~Override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~SetGlobal("BD_PlayerChest","GLOBAL",1)~ ~SetGlobal("BD_PlayerChest","GLOBAL",1) SetGlobal("#L_PlayerChestLocation","GLOBAL",11)~
		END
	COPY_EXISTING ~BD7100.BCS~ ~Override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~SetGlobal("BD_PlayerChest","GLOBAL",2)~ ~SetGlobal("BD_PlayerChest","GLOBAL",2) SetGlobal("#L_PlayerChestLocation","GLOBAL",12)~
		END
	COPY_EXISTING ~BD3000.BCS~ ~Override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~SetGlobal("BD_PlayerChest","GLOBAL",3)~ ~SetGlobal("BD_PlayerChest","GLOBAL",3) SetGlobal("#L_PlayerChestLocation","GLOBAL",13)~
		END
END