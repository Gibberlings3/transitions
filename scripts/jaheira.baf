///////////////////////////////////////////////////////
// Kicks off post-korlasz and pre-SoD exit dialogues //
// Parsed via main_common.tpa                        //
///////////////////////////////////////////////////////

// Post-Korlasz Pre-EndOfBG1 In Party
IF
	Global("#L_SarvQuests","GLOBAL",99)
	Global("#L_JaheiraModded","GLOBAL",0)
	Global("EndOfBG1","GLOBAL",0)
	Global("#L_JaheiraPK","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!AreaType(DUNGEON)
	CombatCounter(0)
THEN
	RESPONSE #100
		ClearAllActions()
		SetGlobal("#L_JaheiraPK","GLOBAL",1)
		Dialogue(Player1)
END

// Post-Korlasz Pre-EndOfBG1 Not In Party
IF
	Global("#L_SarvQuests","GLOBAL",99)
	Global("EndOfBG1","GLOBAL",0)
	Global("#L_JaheiraModded","GLOBAL",0)
	!InPartyAllowDead(MYSELF)
	See(Player1)
	CombatCounter(0)
THEN
	RESPONSE #100
		ClearAllActions()
		Dialogue(Player1)
END

// Pre-SoD
IF
	GlobalGT("#L_StartCaelarAttack","GLOBAL",0)
	Global("EndOfBG1","GLOBAL",1)
	Global("#L_JaheiraModded","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	OR(2)
		Global("#L_NPCSoD","GLOBAL",0)
		GlobalTimerExpired("#L_NPCSoD","GLOBAL")
	!AreaType(DUNGEON)
	CombatCounter(0)
THEN
	RESPONSE #100
		ClearAllActions()
		Dialogue(Player1)
END

// Post BG1
IF
	GlobalGT("EndOfBG1","GLOBAL",0)
	Global("#L_JaheiraOkInBG1Areas","GLOBAL",0)
	Global("#L_JaheiraModded","GLOBAL",0)
	!InPartyAllowDead(MYSELF)
	AreaType(BG1AREA)
THEN
	RESPONSE #100
		SetGlobal("#L_JaheiraOkInBG1Areas","GLOBAL",1)
		DestroySelf()
END
