///////////////////////////////////////////////////////////
// Kicks off post-korlasz and pre-SoD/BG2 exit dialogues //
// Parsed via main_common.tpa                            //
///////////////////////////////////////////////////////////

// Post-Korlasz Pre-EndOfBG1 In Party
IF
	Global("#L_SarvQuests","GLOBAL",99)
	Global("#L_BaelothModded","GLOBAL",0)
	Global("EndOfBG1","GLOBAL",0)
	Global("#L_BaelothPK","GLOBAL",0)
	Global("#L_NPCPK","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!AreaType(DUNGEON)
	CombatCounter(0)
THEN
	RESPONSE #100
		ClearAllActions()
		SetGlobal("#L_BaelothPK","GLOBAL",1)
		SetGlobal("#L_NPCPK","GLOBAL",1)
		Dialogue(Player1)
END

// Post-Korlasz Pre-EndOfBG1 Not In Party
IF
	Global("#L_SarvQuests","GLOBAL",99)
	Global("EndOfBG1","GLOBAL",0)
	Global("#L_BaelothModded","GLOBAL",0)
	!InPartyAllowDead(MYSELF)
	See(Player1)
	CombatCounter(0)
THEN
	RESPONSE #100
		Dialogue(Player1)
END

// Pre-SoD
IF
	GlobalGT("#L_StartCaelarAttack","GLOBAL",0)
	Global("EndOfBG1","GLOBAL",1)
	Global("#L_BaelothModded","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	OR(2)
		Global("#L_NPCSoD","GLOBAL",0)
		GlobalTimerExpired("#L_NPCSoD","GLOBAL")
	!AreaType(DUNGEON)
	CombatCounter(0)
THEN
	RESPONSE #100
		ClearAllActions()
		Dialogue(Player1)
END

// Pre-BG2
IF
	Global("#L_StartBG2","GLOBAL",1)
	Global("#L_BaelothModded","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	OR(2)
		Global("#L_NPCBG2","GLOBAL",0)
		GlobalTimerExpired("#L_NPCBG2","GLOBAL")
	!AreaType(DUNGEON)
	CombatCounter(0)
THEN
	RESPONSE #100
		ClearAllActions()
		Dialogue(Player1)
END

// Post BG1
IF
	GlobalGT("EndOfBG1","GLOBAL",0)
	Global("#L_BaelothOkInBG1Areas","GLOBAL",0)
	Global("#L_BaelothModded","GLOBAL",0)
	!InPartyAllowDead(MYSELF)
	AreaType(BG1AREA)
THEN
	RESPONSE #100
		SetGlobal("#L_BaelothOkInBG1Areas","GLOBAL",1)
		DestroySelf()
END
