//////////////////////////////////
// Angelo and Winski Quests     //
// Parsed via quests_common.tpa //
//////////////////////////////////
IF
	Global("#L_SarvQuests","GLOBAL",99)
	OR(3)
		Exists("#LffEsc1")
		Exists("#LffEsc2")
		Exists("#LffEsc3")
THEN
	RESPONSE #100
		ActionOverride("#LffEsc1",DestroySelf())
		ActionOverride("#LffEsc2",DestroySelf())
		ActionOverride("#LffEsc3",DestroySelf())
		Continue()
END

IF
	Global("#L_SarvQuests","GLOBAL",1)
	Global("#L_FFSpawned","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_FFSpawned","MYAREA",1)
		// 3 Flaming Fist outside the sewer entrance
		CreateCreature("#LffEsc1",[345.750],S)
		CreateCreature("#LffEsc2",[380.745],S)
		CreateCreature("#LffEsc3",[415.715],S)
		Continue()
END

IF
	Global("#L_SarvQuests","GLOBAL",4)
	GlobalLT("#L_FFSpawned","MYAREA",2)
THEN
	RESPONSE #100
		SetGlobal("#L_FFSpawned","MYAREA",2)
		// 3 Flaming Fist outside the sewer entrance
		CreateCreature("#LffEsc1",[1350.2160],S)
		CreateCreature("#LffEsc2",[1330.2220],E)
		CreateCreature("#LffEsc3",[1465.2185],S)
		Continue()
END

IF
	Global("#L_SarvQuests","GLOBAL",1)
	Dead("GALDOR")
	TriggerOverride("#LffEsc2",See([PC]))
	Global("#L_Quest1Cleanup","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_Quest1Cleanup","MYAREA",1)
		ActionOverride("#LffEsc2",StartDialogueNoSet(Player1))
		Continue()
END

IF
	Global("#L_SarvQuests","GLOBAL",4)
	Dead("WINSKI2")
	TriggerOverride("#LffEsc2",See([PC]))
	Global("#L_Quest4Cleanup","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_Quest4Cleanup","MYAREA",1)
		ActionOverride("#LffEsc2",StartDialogueNoSet(Player1))
		Continue()
END

