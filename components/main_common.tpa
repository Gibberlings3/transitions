/////////////////////////////////////////////////////////
// Actions common to all options of the main component //
// Included via main0-2.tpa but only once              //
/////////////////////////////////////////////////////////

ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1/c#endlessbg1.tp2~ 0) BEGIN
   // Remove remnant of block left behind by jastey
   LAF DELETE_SCRIPT_BLOCK
      INT_VAR
         only_once = 0
      STR_VAR
         script = BG0125
         match = ~StartCutSceneEx("BDSODTRN",TRUE)~
   END
   // Remove jastey's code to prep the dukes in the palace - we do it differently
   LAF DELETE_SCRIPT_BLOCK
      INT_VAR
         only_once = 0
      STR_VAR
         script = BG0108
         match = ~C#st_RearrangeDukes~
   END
END

// 3 Flaming Fist escort ... used in various places
COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc1.cre~
   SAY NAME1 @1007 /* ~Flaming Fist Escort~ */
   SAY NAME2 @1007 /* ~Flaming Fist Escort~ */
   WRITE_ASCII 0x280 ~#LffEsc1~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
   WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
   WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc2.cre~
   SAY NAME1 @1008 // ~Flaming Fist Escort~
   SAY NAME2 @1008 // ~Flaming Fist Escort~
   WRITE_ASCII 0x280 ~#LffEsc2~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
   WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
   WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~FLAM11.cre~ ~override/#LffEsc3.cre~
   SAY NAME1 @1008 // ~Flaming Fist Escort~
   SAY NAME2 @1008 // ~Flaming Fist Escort~
   WRITE_ASCII 0x280 ~#LffEsc3~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LffEsc1~ #8   // dialogue
   WRITE_EVALUATED_ASCII 0x250 ~~ #8   // class script - nada
   WRITE_EVALUATED_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander
BUT_ONLY_IF_IT_CHANGES

// Dialogues for the Flaming Fist escort that are used for various components
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/ffescort.d~ USING ~%tra_loc%/%s/dialogues.tra~

// New general script file for new flaming fist healers et al
// Stops them from wandering around when first spawned
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LffGen.baf~

// Palace personnel dialogues
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/palace_personnel.d~ USING ~%tra_loc%/%s/dialogues.tra~

// New general script file for new palace servant
// Stops it from wandering around when first spawned
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LSrvGen.baf~

// Palace gate guards
COPY_EXISTING ~flambrid.cre~ ~override/#LDPGrd1.cre~
   WRITE_ASCII 0x280 ~#LDPGrd1~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LDPGrd1~ #8 // set dialogue file

COPY_EXISTING ~flambrid.cre~ ~override/#LDPGrd2.cre~
   WRITE_ASCII 0x280 ~#LDPGrd2~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LDPGrd1~ #8 // set dialogue file

// Palace servant on the 3rd floor
COPY_EXISTING ~servan.cre~ ~override/#LDPSrv1.cre~
   WRITE_ASCII 0x280 ~#LDPSrv1~ #32 // set DV
   WRITE_ASCII 0x2cc ~#LDPSrv1~ #8 // set dialogue file
   WRITE_ASCII 0x260 ~#LffGen~ #8  // general script - don't wander

// Open the palace gates
// And place friendly guards
COPY_EXISTING ~BG0200.BCS~ ~override~
   DECOMPILE_AND_PATCH BEGIN 
      APPEND_FILE TEXT ~%mod_root%/scripts/BG0200a.baf~
   END
BUT_ONLY_IF_IT_CHANGES

// Place the servant in the custom SoD version of the third floor
// This is a common script used in all PC personal quarters
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LPCRoom.baf~

// Modified copy of SoD's version of the third floor
COPY ~%mod_root%/areas/#LBD0103.ARE~ ~override~ 

// Open the way to the PCs rooms
LAF UPDATE_BCS
   STR_VAR
      baf_location = EVAL ~%mod_root%/scripts/~
      baf_name = ~BG0109~
      bcs_name = ~BG0109~
END

// Add 2 new inactive exit travel triggers leading from the 2nd floor to the new 3rd floor
// Activate the new ones and deactive the original ones when/if the PC and/or Imoen take up residence
COPY_EXISTING ~BG0109.ARE~ ~override~
   LPF GW_CLONE_TRAVEL_TRIGGER
      INT_VAR
         GW_Flags_new = 256 // Deactivated
      STR_VAR
         GW_Name_old = ~Door0110a~
         GW_Name_new = ~Door#lbd0103a~
         GW_Dest_new = ~#LBD0103~
         GW_Entre_new = ~ExitBG0109a~
         GW_Key_new = ~~      // Clear out the key if one exists
         GW_Script_new = ~~   // Clear out the script if one exists
   END

   LPF GW_CLONE_TRAVEL_TRIGGER
      INT_VAR
         GW_Flags_new = 256 // Deactivated
      STR_VAR
         GW_Name_old = ~Door0110b~
         GW_Name_new = ~Door#lbd0103b~
         GW_Dest_new = ~#LBD0103~
         GW_Entre_new = ~ExitBG0109b~
         GW_Key_new = ~~      // Clear out the key if one exists
         GW_Script_new = ~~   // Clear out the script if one exists
   END
BUT_ONLY_IF_IT_CHANGES

// Update Duke Belt's dialogue file
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/duke_belt.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Update Duke Liia's dialogue file
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/duke_liia.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Update Imoen's dialogue file and script to initiate pre-sod exit dialogue
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/imoen_training.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~IMOEN_.BCS~ ~%mod_root%/scripts/imoen.baf~

// Update Duke Belt's override script so he'll initiate conversation when PC first arrives in palace
EXTEND_TOP ~BELT.BCS~ ~%mod_root%/scripts/Belt_Add.baf~

// Make sure Duke Belt and Liia are alive and in position inside the Ducal Palace
// Also sets up dialogue cues
COPY_EXISTING ~BG0108.BCS~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      APPEND_FILE TEXT ~%mod_root%/scripts/BG0108a.baf~
   END
BUT_ONLY_IF_IT_CHANGES

// Add key rings to Black Lily, Silence, Sorcery Sundries (both BG1 and SoD versions), 
// the quartermaster in SoD and Ribald's in BG2 just for good measure
COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR01.ITM~ 
   SAY NAME1 @1014 /* ~Black Key Ring~ */
   SAY NAME2 @1014 /* ~Black Key Ring~ */
   SAY UNIDENTIFIED_DESC @1020
   SAY IDENTIFIED_DESC @1020
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR01.STO~ 
   SAY 0x0c @1014
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~STOBLACK.sto~ ~override~   // Black Lily
   ADD_STORE_ITEM ~#LKEYR01~ AFTER ~SLNG11~ #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR02.ITM~ 
   SAY NAME1 @1015 /* ~Silver Key Ring~ */
   SAY NAME2 @1015 /* ~Silver Key Ring~ */
   SAY UNIDENTIFIED_DESC @1021
   SAY IDENTIFIED_DESC @1021
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR02.STO~ 
   SAY 0x0c @1015
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~STO0703.STO~ ~override~  // BG Sorcerer's Sundries
   ADD_STORE_ITEM ~#LKEYR02~ LAST #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR03.ITM~ 
   SAY NAME1 @1016 /* ~Brass Key Ring~ */
   SAY NAME2 @1016 /* ~Brass Key Ring~ */
   SAY UNIDENTIFIED_DESC @1022
   SAY IDENTIFIED_DESC @1022
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR03.STO~ 
   SAY 0x0c @1016
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKAZZRE.STO~ ~override~  // Kazzrem inside BD Sorcerer's Sundries
   ADD_STORE_ITEM ~#LKEYR03~ LAST #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR04.ITM~ 
   SAY NAME1 @1017 /* ~Steel Key Ring~ */
   SAY NAME2 @1017 /* ~Steel Key Ring~ */
   // Current description already says steel
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR04.STO~ 
   SAY 0x0c @1017
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDBELEG4.sto~ ~override~   // Quartermaster Belegarm
   ADD_STORE_ITEM ~#LKEYR04~ AFTER ~BAG31~ #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR05.ITM~ 
   SAY NAME1 @1018 /* ~Iron Key Ring~ */
   SAY NAME2 @1018 /* ~Iron Key Ring~ */
   SAY UNIDENTIFIED_DESC @1023
   SAY IDENTIFIED_DESC @1023
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR05.STO~ 
   SAY 0x0c @1018
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~STOSILEN.sto~ ~override~   // Silence
   ADD_STORE_ITEM ~#LKEYR05~ LAST #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~BDKEYR.ITM~ ~override/#LKEYR06.ITM~ 
   SAY NAME1 @1019 /* ~Copper Key Ring~ */
   SAY NAME2 @1019 /* ~Copper Key Ring~ */
   SAY UNIDENTIFIED_DESC @1024
   SAY IDENTIFIED_DESC @1024
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~BDKEYR.STO~ ~override/#LKEYR06.STO~ 
   SAY 0x0c @1019
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~RIBALD.sto~ ~override~   // Ribald's Adventurer Mart
   ADD_STORE_ITEM ~#LKEYR06~ AFTER ~BAG02~ #0 #0 #0 ~IDENTIFIED~ #1
BUT_ONLY_IF_IT_CHANGES

// New cut scene called from Duke Belt's dialogue for Korlasz quest
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Cut04.baf~

// Update BDRope.bcs so that when someone clicks on it, it'll take the party to the surface
// instead of starting the ending dialogue
COPY_EXISTING ~BDRope.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Clicked([ANYONE])~ ~Clicked([ANYONE]) !Global("#L_SarvQuests","GLOBAL",8)~
   END
BUT_ONLY_IF_IT_CHANGES
EXTEND_TOP ~BDRope.bcs~ ~%mod_root%/scripts/BDRope.baf~

// Update 1st Korlasz Safehouse area script so no group or equipment swaps
COPY_EXISTING ~BD0120.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("K#NewGame","BD0120",0)~ ~Global("K#NewGame","BD0120",0) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY ~Global("K#StoryMode","BD0120",0)~ ~Global("K#StoryMode","BD0120",0) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY ~GlobalLT("bd_plot","global",2)~ ~GlobalLT("bd_plot","global",2) !Global("#L_SarvQuests","GLOBAL",8)~
      REPLACE_TEXTUALLY ~!InMyArea("IMOEN2")~ ~!InMyArea("IMOEN2") !Global("#L_SarvQuests","GLOBAL",8)~
   END
BUT_ONLY_IF_IT_CHANGES

// Use a modified intro scene
LAF INSERT_SCRIPT_BLOCK
   INT_VAR 
      insert_above = 0
      only_once = 1
   STR_VAR 
      script = BD0120
      match = ~GlobalLT("bd_plot","global",2)~
      match1 = ~StartCutSceneEx("bdintro",TRUE)~
      insert = ~%mod_root%/scripts/bd0120.baf~
END
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Cut05.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Skip.baf~

// Updates to 2nd Korlasz Safehouse area script to sidestep ending quest hand-holding
COPY_EXISTING ~BD0130.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~!Global("BD_Korlasz_Fight","BD0130",2)~ ~!Global("BD_Korlasz_Fight","BD0130",2) !Global("#L_SarvQuests","GLOBAL",8)~
   END
BUT_ONLY_IF_IT_CHANGES
// Replacement block for the one side-stepped above
LAF INSERT_SCRIPT_BLOCK
   INT_VAR 
      insert_above = 0
      only_once = 1
   STR_VAR 
      script = BD0130
      match = ~!Global("BD_Korlasz_Fight","BD0130",2)~
      insert = ~%mod_root%/scripts/bd0130.baf~
END

// Modified dialogues for Flaming Fist escorts in Korlasz Dungeon
// Removes calls to Imoen's dialogue if doing quest
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/bdff100x.d~ USING ~%tra_loc%/%s/dialogues.tra~

// Modified version of Korlasz's Journal with no mention of refugees
COPY_EXISTING ~BDSHKORJ.ITM~ ~override~
   SAY UNIDENTIFIED_DESC @1027
   SAY IDENTIFIED_DESC @1027
BUT_ONLY_IF_IT_CHANGES

// Add an info area where the group exits from Korlasz's family crypt
// Can't say I'm completely happy with this, but it's better than nothing
COPY_EXISTING ~BG0200.ARE~ ~override~
   LPF ADD_AREA_REGION_TRIGGER
      INT_VAR
         ab_RT_Type = 1
         ab_RT_BbLX = 1356
         ab_RT_BbLY = 532
         ab_RT_BbHX = 1558
         ab_RT_BbHY = 619
         ab_RT_VxPr = 4
         ab_RT_Vx_X_0 = 1356
         ab_RT_Vx_Y_0 = 532
         ab_RT_Vx_X_1 = 1368
         ab_RT_Vx_Y_1 = 618
         ab_RT_Vx_X_2 = 1558
         ab_RT_Vx_Y_2 = 619
         ab_RT_Vx_X_3 = 1540
         ab_RT_Vx_Y_3 = 545
         ab_RT_Itxt = RESOLVE_STR_REF (@1028) /* ~Sealed exit from the Korlasz family crypt~ */
      STR_VAR
         ab_RT_Name = "#L_KorlaszExit"
   END
BUT_ONLY_IF_IT_CHANGES

// Prep canned BG1 NPCs for leaving/staying after Korlasz quest and at the beginning of SoD and BG2
// NOTE: Jaheira, Khalid, Minsc, and Dynaheir don't get BG2 exit dialogues, they'll stick with the PC
// Imoen is dealt with separately
ACTION_DEFINE_ARRAY npc_list BEGIN
   Ajantis
   Jaheira
   Khalid
   Dorn
   Edwin
   Neera
   Baeloth
   Garrick
   Xan
   Minsc
   Dynaheir
   Alora
   Branwen
   Coran
   Eldoth
   Montaron
   Xzar
   Faldorn
   Kagain
   Kivan
   Quayle
   Rasaad
   Safana
   Sharteel
   Skie
   Tiax
   Viconia
   Yeslick
END

OUTER_SET npc_num = 0

// Additions to all NPCs (except Imoen)
ACTION_PHP_EACH npc_list AS _ => npc_name BEGIN

   ACTION_IF npc_num < 7 THEN BEGIN
      OUTER_SPRINT pk ~// Post-Korlasz while in party
IF
   Global("#L_SarvQuests","GLOBAL",99)
   Global("#L_%npc_name%Modded","GLOBAL",0)
   Global("EndOfBG1","GLOBAL",0)
   Global("#L_%npc_name%PK","GLOBAL",0)
   Global("#L_NPCPK","GLOBAL",0)
   IsValidForPartyDialogue(MYSELF)
   !AreaType(DUNGEON)
   CombatCounter(0)
THEN
   RESPONSE #100
      ClearAllActions()
      SetGlobal("#L_%npc_name%PK","GLOBAL",1)
      SetGlobal("#L_NPCPK","GLOBAL",1)
      Dialogue(Player1)
END

// Post-Korlasz while not in party
IF
   Global("#L_SarvQuests","GLOBAL",99)
   Global("EndOfBG1","GLOBAL",0)
   Global("#L_%npc_name%Modded","GLOBAL",0)
   !InPartyAllowDead(MYSELF)
   See(Player1)
   CombatCounter(0)
THEN
   RESPONSE #100
      Dialogue(Player1)
END
~
   END ELSE BEGIN
      OUTER_SPRINT pk ~~
   END

   ACTION_IF (%npc_num% = 1) OR (%npc_num% = 2) OR (%npc_num% = 9) OR (%npc_num% = 10) THEN BEGIN
      OUTER_SPRINT bg ~~
   END ELSE BEGIN
      OUTER_SPRINT bg ~// Pre-BG2
IF
   Global("#L_StartBG2","GLOBAL",1)
   Global("#L_%npc_name%Modded","GLOBAL",0)
   IsValidForPartyDialogue(MYSELF)
   OR(2)
      Global("#L_NPCBG2","GLOBAL",0)
      GlobalTimerExpired("#L_NPCBG2","GLOBAL")
   !AreaType(DUNGEON)
   CombatCounter(0)
THEN
   RESPONSE #100
      ClearAllActions()
      Dialogue(Player1)
END
~
   END

   <<<<<<<< .../npc_add.baf
   // Post-Korlasz
   %pk%

   // Pre-SoD
   IF
      GlobalGT("#L_StartCaelarAttack","GLOBAL",0)
      Global("EndOfBG1","GLOBAL",1)
      Global("#L_%npc_name%Modded","GLOBAL",0)      // NPC isn't modded
      IsValidForPartyDialogue(MYSELF)
      OR(2)
         Global("#L_NPCSoD","GLOBAL",0)
         GlobalTimerExpired("#L_NPCSoD","GLOBAL")
      !AreaType(DUNGEON)
      CombatCounter(0)
      OR(2)
         GlobalGT("#L_StartCaelarAttack","GLOBAL",2)     // Imoen staying in the group
         !InPartyAllowDead("IMOEN2")
   THEN
      RESPONSE #100
         ClearAllActions()
         Dialogue(Player1)
   END

   // Pre-BG2
   %bg%

   // Post BG1
   IF
      GlobalGT("EndOfBG1","GLOBAL",0)
      Global("#L_%npc_name%OkInBG1Areas","GLOBAL",0)
      Global("#L_%npc_name%Modded","GLOBAL",0)
      !InPartyAllowDead(MYSELF)
      AreaType(BG1AREA)
   THEN
      RESPONSE #100
         SetGlobal("#L_%npc_name%OkInBG1Areas","GLOBAL",1)
         DestroySelf()
   END
   >>>>>>>>
   EXTEND_TOP ~%npc_name%.bcs~ ~.../npc_add.baf~ EVALUATE_BUFFER

   // Exit dialogues

   // Initial post_Korlasz interjections
   OUTER_SPRINT pk_i1 ~~~~~== AJANTJ IF ~IsValidForPartyDialogue("AJANTIS") Global("#L_AjantisModded","GLOBAL",0)~ @2340 /* ~Me as well.~ */~~~~~
   OUTER_SPRINT pk_i2 ~~~~~== JAHEIJ IF ~IsValidForPartyDialogue("JAHEIRA") Global("#L_JaheriaModded","GLOBAL",0)~ @2326 /* ~I too have other places to be.~ */~~~~~
   OUTER_SPRINT pk_i3 ~~~~~== KHALIJ IF ~IsValidForPartyDialogue("JAHEIRA") Global("#L_JaheriaModded","GLOBAL",0) IsValidForPartyDialogue("KHALID") Global("#L_KhalidModded","GLOBAL",0)~ @2338 /* ~Yes, *we* should go.~ */ 
      == KHALIJ IF ~OR(2) !IsValidForPartyDialogue("JAHEIRA") !Global("#L_JaheriaModded","GLOBAL",0) IsValidForPartyDialogue("KHALID") Global("#L_KhalidModded","GLOBAL",0)~ @2332 /* ~I-I should be going as well.~ */~~~~~
   OUTER_SPRINT pk_i4 ~~~~~== DORNJ_ IF ~IsValidForPartyDialogue("DORN") Global("#L_DornModded","GLOBAL",0)~ @2340   /* ~Me as well.~ */~~~~~
   OUTER_SPRINT pk_i5 ~~~~~== EDWINJ_ IF ~IsValidForPartyDialogue("EDWIN") Global("#L_EdwinModded","GLOBAL",0)~ @2347 /* ~(gives you an exaggerated yawn)~ */~~~~~
   OUTER_SPRINT pk_i6 ~~~~~== NEERAJ_ IF ~IsValidForPartyDialogue("NEERA") Global("#L_NeeraModded","GLOBAL",0)~ @2354 /* ~I do have plans, yes.~ */~~~~~
   OUTER_SPRINT pk_i7 ~~~~~== BAELOTHJ IF ~IsValidForPartyDialogue("BAELOTH") Global("#L_BaelothModded","GLOBAL",0)~ @2360 /* ~You hog all the limelight.~ */~~~~~

   // Staying after Korlasz interjections
   OUTER_SPRINT pk_s1 ~~~~~== AJANTJ IF ~IsValidForPartyDialogue("AJANTIS") Global("#L_AjantisModded","GLOBAL",0)~ @2283 /* ~As you wish.  I owe you that at least.~ */~~~~~
   OUTER_SPRINT pk_s2 ~~~~~== JAHEIJ IF ~IsValidForPartyDialogue("JAHEIRA") Global("#L_JaheriaModded","GLOBAL",0)~ @2327 /* ~So be it.~ */~~~~~
   OUTER_SPRINT pk_s3 ~~~~~== KHALIJ IF ~IsValidForPartyDialogue("KHALID") Global("#L_KhalidModded","GLOBAL",0)~ @2333  /* ~If you insist.~ */~~~~~
   OUTER_SPRINT pk_s4 ~~~~~== DORNJ_ IF ~IsValidForPartyDialogue("DORN") Global("#L_DornModded","GLOBAL",0)~ @2341   /* ~I can stay for a little while.~ */~~~~~
   OUTER_SPRINT pk_s5 ~~~~~== EDWINJ_ IF ~IsValidForPartyDialogue("EDWIN") Global("#L_EdwinModded","GLOBAL",0)~ @2348 /* ~Fine. (sigh)~ */~~~~~
   OUTER_SPRINT pk_s6 ~~~~~== NEERAJ_ IF ~IsValidForPartyDialogue("NEERA") Global("#L_NeeraModded","GLOBAL",0)~ @2355 /* ~Ok, for a little while.~ */~~~~~
   OUTER_SPRINT pk_s7 ~~~~~== BAELOTHJ IF ~IsValidForPartyDialogue("BAELOTH") Global("#L_BaelothModded","GLOBAL",0)~ @2361 /* ~Have it your way, as always!~ */~~~~~

   // Leaving after Korlasz interjections
   OUTER_SPRINT pk_l1 ~~~~~== AJANTJ IF ~IsValidForPartyDialogue("AJANTIS") Global("#L_AjantisModded","GLOBAL",0)~ @2284 DO ~SetGlobal("#L_AjantisOkInBG1Areas","GLOBAL",1) ActionOverride("AJANTIS",LeaveParty()) ActionOverride("AJANTIS",EscapeArea())~ /* ~It has been an honor.  Farewell.~ */~~~~~
   OUTER_SPRINT pk_l2 ~~~~~== JAHEIJ IF ~IsValidForPartyDialogue("JAHEIRA") Global("#L_JaheriaModded","GLOBAL",0)~ @2328 DO ~SetGlobal("#L_JaheiraOkInBG1Areas","GLOBAL",1) ActionOverride("JAHEIRA",LeaveParty()) ActionOverride("JAHEIRA",EscapeArea())~ /* ~Take care of yourself.~ */~~~~~
   OUTER_SPRINT pk_l3 ~~~~~== KHALIJ IF ~IsValidForPartyDialogue("KHALID") Global("#L_KhalidModded","GLOBAL",0)~ @2334 DO ~SetGlobal("#L_KhalidOkInBG1Areas","GLOBAL",1) ActionOverride("KHALID",LeaveParty()) ActionOverride("KHALID",EscapeArea())~ /* ~Goodbye.~ */~~~~~
   OUTER_SPRINT pk_l4 ~~~~~== DORNJ_ IF ~IsValidForPartyDialogue("DORN") Global("#L_DornModded","GLOBAL",0)~ @2346 DO ~SetGlobal("#L_DornOkInBG1Areas","GLOBAL",1) ActionOverride("DORN",LeaveParty()) ActionOverride("DORN",EscapeArea())~   /* ~Until we meet again.~ */~~~~~
   OUTER_SPRINT pk_l5 ~~~~~== EDWINJ_ IF ~IsValidForPartyDialogue("EDWIN") Global("#L_EdwinModded","GLOBAL",0)~ @2349 DO ~SetGlobal("#L_EdwinOkInBG1Areas","GLOBAL",1) ActionOverride("EDWIN",LeaveParty()) ActionOverride("EDWIN",EscapeArea())~ /* ~Finally!~ */~~~~~
   OUTER_SPRINT pk_l6 ~~~~~== NEERAJ_ IF ~IsValidForPartyDialogue("NEERA") Global("#L_NeeraModded","GLOBAL",0)~ @2356 DO ~SetGlobal("#L_NeeraOkInBG1Areas","GLOBAL",1) ActionOverride("NEERA",LeaveParty()) ActionOverride("NEERA",EscapeArea())~ /* ~See ya!  Good luck and all.~ */~~~~~
   OUTER_SPRINT pk_l7 ~~~~~== BAELOTHJ IF ~IsValidForPartyDialogue("BAELOTH") Global("#L_BaelothModded","GLOBAL",0)~ @2362 DO ~SetGlobal("#L_BaelothOkInBG1Areas","GLOBAL",1) ActionOverride("BAELOTH",LeaveParty()) ActionOverride("BAELOTH",EscapeArea())~ /* ~Look for my name in lights, I'm bowing out of this dump!~ */~~~~~

   ACTION_MATCH %npc_num% WITH
      0 BEGIN
         OUTER_SPRINT npc_jfile ~AJANTJ~
         OUTER_SPRINT pk_init ~@2278~ // I am eager to present our accomplishments before my superiors, but I will stay if you need me.
         OUTER_SPRINT pk_i1 ~~
         OUTER_SPRINT pk_stay ~@2283~ // As you wish.  I owe you that at least.
         OUTER_SPRINT pk_s1 ~~
         OUTER_SPRINT pk_leave ~@2284~ // It has been an honor.  Farewell.
         OUTER_SPRINT pk_l1 ~~
      END
      1 BEGIN
         OUTER_SPRINT npc_jfile ~JAHEIJ~
         OUTER_SPRINT pk_init ~@2330~ // <CHARNAME>, you are safe now and I have places I should be, but I will stay if you have need of me.
         OUTER_SPRINT pk_i2 ~~
         OUTER_SPRINT pk_stay ~@2327~ // So be it.
         OUTER_SPRINT pk_s2 ~~
         OUTER_SPRINT pk_leave ~@2328~ // Take care of yourself.
         OUTER_SPRINT pk_l2 ~~
      END
      2 BEGIN
         OUTER_SPRINT npc_jfile ~KHALIJ~
         OUTER_SPRINT pk_init ~@2336~ // <CHARNAME>, you are s-safe now and I have p-places I should be, but I will s-stay if you have need of me.
         OUTER_SPRINT pk_i3 ~~
         OUTER_SPRINT pk_stay ~@2333~ // If you insist.
         OUTER_SPRINT pk_s3 ~~
         OUTER_SPRINT pk_leave ~@2334~ // Goodbye.
         OUTER_SPRINT pk_l3 ~~
      END
      3 BEGIN
         OUTER_SPRINT npc_jfile ~DORNJ_~
         OUTER_SPRINT pk_init ~@2344~ // <CHARNAME>, my patron would have me be elsewhere soon, but I can stay for short while if you need me.
         OUTER_SPRINT pk_i4 ~~
         OUTER_SPRINT pk_stay ~@2341~ // I can stay for a little while.
         OUTER_SPRINT pk_s4 ~~
         OUTER_SPRINT pk_leave ~@2342~ // I feel we will meet again.
         OUTER_SPRINT pk_l4 ~~
      END
      4 BEGIN
         OUTER_SPRINT npc_jfile ~EDWINJ_~
         OUTER_SPRINT pk_init ~@2351~ // I've just about had all I can take of this.  Are we just about done?
         OUTER_SPRINT pk_i5 ~~
         OUTER_SPRINT pk_stay ~@2348~ // Fine. (sigh)
         OUTER_SPRINT pk_s5 ~~
         OUTER_SPRINT pk_leave ~@2349~ // Finally!
         OUTER_SPRINT pk_l5 ~~
      END
      5 BEGIN
         OUTER_SPRINT npc_jfile ~NEERAJ_~
         OUTER_SPRINT pk_init ~@2358~ // You're in the clear now, right?  Can I go?
         OUTER_SPRINT pk_i6 ~~
         OUTER_SPRINT pk_stay ~@2355~ // Ok, for a little while.
         OUTER_SPRINT pk_s6 ~~
         OUTER_SPRINT pk_leave ~@2356~ // See ya!  Good luck and all.
         OUTER_SPRINT pk_l6 ~~
      END
      6 BEGIN
         OUTER_SPRINT npc_jfile ~BAELOTHJ~
         OUTER_SPRINT pk_init ~@2364~ // This bantering about Baldur's Gate is beyond boring! I have plans.  Huge plans!
         OUTER_SPRINT pk_i7 ~~
         OUTER_SPRINT pk_stay ~@2361~ // Have it your way, as always!~
         OUTER_SPRINT pk_s7 ~~
         OUTER_SPRINT pk_leave ~@2362~ // Look for my name in lights, I'm bowing out of this dump!      
         OUTER_SPRINT pk_l7 ~~
      END
      DEFAULT
         OUTER_SPRINT pk_init ~~
   END

   ACTION_IF npc_num < 7 THEN BEGIN
      <<<<<<<< .../npc_add.d

      CHAIN
         IF WEIGHT #-999 ~Global("#L_%npc_name%Modded","GLOBAL",0) Global("EndOfBG1","GLOBAL",0) Global("#L_SarvQuests","GLOBAL",99) GlobalLT("#L_NPCPK","GLOBAL",2) !AreaType(DUNGEON) CombatCounter(0)~ THEN %npc_jfile% POST_KORLASZ_ASK
            %pk_init%
            %pk_i1%
            %pk_i2%
            %pk_i3%
            %pk_i4%
            %pk_i5%
            %pk_i6%
            %pk_i7%
         END
         + ~!Global("#L_LetsHaveFun","GLOBAL",1)~ + @2279 /* ~Please don't go yet.  I still need you, for just a few more days.~ */ + POST_KORLASZ_STAY
         + ~Global("#L_LetsHaveFun","GLOBAL",1)~ + @2280 /* ~Please let me treat you to a little fun before you go.  Will a day or two make that much difference?~ */  + POST_KORLASZ_STAY
         ++ @2281 /* ~Of course you may leave, but not with that equipment.~ */  + POST_KORLASZ_STAY
         ++ @2282 /* ~I understand.  Now is as good a time as any to say our farewells.~ */ + POST_KORLASZ_LEAVE

      CHAIN
         IF ~~ THEN %npc_jfile% POST_KORLASZ_STAY
            %pk_stay%
            %pk_s1%
            %pk_s2%
            %pk_s3%
            %pk_s4%
            %pk_s5%
            %pk_s6%
            %pk_s7%
         END
         IF ~~ THEN DO ~SetGlobal("#L_NPCPK","GLOBAL",2)~ EXIT

      CHAIN
         IF ~~ THEN %npc_jfile% POST_KORLASZ_LEAVE
            %pk_leave%
            %pk_l1%
            %pk_l2%
            %pk_l3%
            %pk_l4%
            %pk_l5%
            %pk_l6%
            %pk_l7%
         END
         IF ~~ THEN DO ~SetGlobal("#L_NPCPK","GLOBAL",3) SetGlobal("#L_%npc_name%OkInBG1Areas","GLOBAL",1) ActionOverride("%npc_name%",LeaveParty()) ActionOverride("%npc_name%",EscapeArea())~ EXIT
      >>>>>>>>
      COMPILE EVALUATE_BUFFER ~.../npc_add.d~ USING ~%tra_loc%/%s/dialogues.tra~
   END

   // Initial pre-SoD interjections


   OUTER_SET npc_num = npc_num + 1
END

/*
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/ajantis.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~AJANTIS.BCS~ ~%mod_root%/scripts/ajantis.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/jaheira.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~JAHEIRA_.BCS~ ~%mod_root%/scripts/jaheira.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/khalid.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~KHALID.BCS~ ~%mod_root%/scripts/khalid.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dorn.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~DORN_.BCS~ ~%mod_root%/scripts/dorn.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/edwin.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~EDWIN_.BCS~ ~%mod_root%/scripts/edwin.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/neera.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~NEERA_.BCS~ ~%mod_root%/scripts/neera.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/baeloth.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~BAELOTH.BCS~ ~%mod_root%/scripts/baeloth.baf~

// The rest of these don't get post-Korlasz exit dialogues
// They have no compelling reason to leave right away
// An argument could be made for Xan, but I think he's not as eager as Ajantis to get going
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/garrick.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~GARRICK_.BCS~ ~%mod_root%/scripts/garrick.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/xan.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~XAN.BCS~ ~%mod_root%/scripts/xan.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/minsc.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~MINSC_.BCS~ ~%mod_root%/scripts/minsc.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dynaheir.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~DYNAHEIR.BCS~ ~%mod_root%/scripts/dynaheir.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/alora.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~ALORA.BCS~ ~%mod_root%/scripts/alora.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/branwen.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~BRANWEN_.BCS~ ~%mod_root%/scripts/branwen.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/coran.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~CORAN.BCS~ ~%mod_root%/scripts/coran.baf~
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/eldoth.d~ USING ~%tra_loc%/%s/dialogues.tra~
EXTEND_TOP ~ELDOTH.BCS~ ~%mod_root%/scripts/eldoth.baf~
*/

// These still need to be done
//EXTEND_TOP ~FALDORN_.BCS~ ~%mod_root%/scripts/faldorn.baf~
//EXTEND_TOP ~KAGAIN.BCS~ ~%mod_root%/scripts/kagain.baf~
//EXTEND_TOP ~KIVAN.BCS~ ~%mod_root%/scripts/kivan.baf~
//EXTEND_TOP ~MONTARON.BCS~ ~%mod_root%/scripts/montaron.baf~
//EXTEND_TOP ~QUAYLE.BCS~ ~%mod_root%/scripts/quayle.baf~
//EXTEND_TOP ~RASAAD_.BCS~ ~%mod_root%/scripts/rasaad.baf~
//EXTEND_TOP ~SAFANA.BCS~ ~%mod_root%/scripts/safana.baf~
//EXTEND_TOP ~SHARTEEL.BCS~ ~%mod_root%/scripts/sharteel.baf~
//EXTEND_TOP ~SKIE_.BCS~ ~%mod_root%/scripts/skie.baf~
//EXTEND_TOP ~TIAX.BCS~ ~%mod_root%/scripts/tiax.baf~
//EXTEND_TOP ~VICONIA_.BCS~ ~%mod_root%/scripts/viconia.baf~
//EXTEND_TOP ~XZAR.BCS~ ~%mod_root%/scripts/xzar.baf~
//EXTEND_TOP ~YESLICK.BCS~ ~%mod_root%/scripts/yeslick.baf~

// The 'magic' item given by Duke Belt that will allow for game advancement
//   and getting to the SoD version of the palace
// PC will find unusable broken remnants of it in Chateau Irenicus
//   if it's in their inventory at the time of capture
// PC will find a 'drained' unusable version of it in their personal chest
//   if it is transfered to their stronghold in BG2
COPY_EXISTING ~COMPON02.ITM~ ~override/#LAdvGm1.itm~ // I like the pretty icon
   SAY NAME1 @1031 /* ~Duke Belt's Gift to <CHARNAME>~ */
   SAY NAME2 @1031 /* ~Duke Belt's Gift to <CHARNAME>~ */
   SAY UNIDENTIFIED_DESC @1032 /* ~This item is able to grant you a limited number of specific wishes.~ */
   SAY IDENTIFIED_DESC @1032 /* ~This item is able to grant you a limited number of specific wishes.~ */
   WRITE_LONG (0x0018) 2093 // Critical Item, Dropable, Displayable, Not Copyable, Conversable
   WRITE_LONG (0x0034) 0 // Price-Discourage it's sale though Critical Item should stop it from being sold
   WRITE_SHORT (0x0042) 0 // Doesn't need a bard to identify

// The dialogue for the item
COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/#LAdvGm1.d~ USING ~%tra_loc%/%s/dialogues.tra~

OUTER_SET UseMe = RESOLVE_STR_REF (@1033)
APPEND itemdial.2da ~#LAdvGm1 %UseMe% #LAdvGm1~ UNLESS ~#LAdvGm1~

// Script to attach to palace exits to start SoD or BG2 when attempting to leave
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LByeBG1.baf~

// Modify palace exits to have a script that will kick off SoD when used
COPY_EXISTING ~BG0108.ARE~ ~override~
   LPF ALTER_AREA_REGION
      STR_VAR
         region_name = ~Door0109a~
         region_script = ~#LByeBG1.BCS~
   END
   LPF ALTER_AREA_REGION
      STR_VAR
         region_name = ~Door0109b~
         region_script = ~#LByeBG1.BCS~
   END
   LPF ALTER_AREA_REGION
      STR_VAR
         region_name = ~Door0111~
         region_script = ~#LByeBG1.BCS~
   END
   LPF ALTER_AREA_REGION
      STR_VAR
         region_name = ~Door0200~
         region_script = ~#LByeBG1.BCS~
   END
BUT_ONLY_IF_IT_CHANGES

// Cut scene that starts SoD
COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#L_Cut07.baf~

// Allow for only 2nd half to be run
LAF INSERT_SCRIPT_BLOCK
   INT_VAR 
      insert_above = 1
      only_once = 1
   STR_VAR 
      script = BD0103
      match = ~TextScreen("DPALACE")~
      insert = ~%mod_root%/scripts/bd0103.baf~
END
COPY_EXISTING ~BD0103.bcs~ ~override~
   DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Global("BD_PARTY_ITEMS","BD0103",0)~ ~Global("BD_PARTY_ITEMS","BD0103",0) !Global("#L_SkipFirstHalfCA","GLOBAL",1)~
   END
BUT_ONLY_IF_IT_CHANGES
