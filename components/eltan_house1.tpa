//////////////////////////////////////////////////////////////////
// Optional component that adds the cabin in High Hedge to      //
// the list of properties available as a reward from Duke Eltan //
// Item strings are in game.tra                                 //
//////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION eltan_house1 BEGIN
	ACTION_IF !(MOD_IS_INSTALLED ~transitions/setup-transitions.tp2~ 60) BEGIN
		INCLUDE ~%mod_root%/components/eltan_common.tpa~
	END

	// Add the key he gives to the protagonist for their new cabin in High Hedge
	COPY ~%mod_root%/objects/#LKey.itm~ ~override/#LKey02.itm~
		SAY NAME1 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY NAME2 @1009 /* ~<CHARNAME>'s House Key~ */
		SAY UNIDENTIFIED_DESC @1011
		SAY IDENTIFIED_DESC @1011

	COPY_EXISTING ~BG3200.ARE~ ~override~
		// Change the name of the door leading into BG3201
		// or else we can't control it because it has the same name 
		// as the travel trigger region
		LPF CHANGE_DOOR_NAME
			STR_VAR
				old_name = ~Door3201~
				new_name = ~Port3201~
		END

		// Clone the travel trigger leading into the house so we can 
		// have the door lead to a custom area
		LPF GW_CLONE_TRAVEL_TRIGGER
			INT_VAR
				GW_Flags_new = 256 // Deactivated
			STR_VAR
				GW_Name_old = ~Door3201~
				GW_Name_new = ~Door#lbg3201~
				GW_Dest_new = ~#LBG3201~
				GW_Key_new = ~~      // Clear out the key if one exists
				GW_Script_new = ~~   // Clear out the script if one exists
		END

		// Need to modify the door object in BG3200 leading into the cabin 
		// in case that one is chosen as their reward
		LPF ALTER_AREA_DOOR
			INT_VAR
				lock_difficulty = 100
			STR_VAR
				door_name = ~Port3201~
				door_open_sound = ~AMB_D22B~
				door_key = ~#LKey02~
		END

		// Move the exit from the house so they aren't locked behind it
		// when they exit (if it's their residence)
		LPF ALTER_AREA_ENTRANCE
			INT_VAR
				x_coord = 1675
				y_coord = 1605
			STR_VAR
				entrance_name = ~Exit3201~
		END
	BUT_ONLY_IF_IT_CHANGES

	// Also need to add a new area that can be attached to the door
	// if/when it is chosen as a reward
	COPY ~%mod_root%/areas/#LBG3201.ARE~ ~override~

	// Update the script used on all PC residences to include the one in front of the ducal palace
	COPY_EXISTING ~#LPCRoom.BCS~ ~override~
	   DECOMPILE_AND_PATCH BEGIN 
			// Move the contents from this house's chest to the ducal palace chest when they go there
			REPLACE_TEXTUALLY ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1)~ ~SetGlobal("#L_PlayerChestLocation","GLOBAL",1) MoveContainerContents("#LBG3201*PlayerChest00","#LBD0103*PlayerChest00")~

			// Add the block where the ducal palace chest contents get move to the house
			APPEND_FILE TEXT ~%mod_root%/scripts/#LPCRoomb.baf~
		END
	BUT_ONLY_IF_IT_CHANGES

	// Place the servant differently than the first 2 areas
	EXTEND_TOP ~#LPCRoom.BCS~ ~%mod_root%/scripts/#LPCRoomb2.baf~

	// Prep PC's cabin in High Hedge
	LAF UPDATE_BCS
		STR_VAR 
			baf_location = EVAL ~%mod_root%/scripts/~
			baf_name = ~BG3200~
			bcs_name = ~BG3200~
	END

	// Update the dialogue of the servant who welcomes them
	COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/house1_servant.d~ USING ~%tra_loc%/%s/dialogues.tra~

	// Set the global to indicate that the cabin in High Hedge is available as a reward
	EXTEND_TOP ~BG0108.BCS~ ~%mod_root%/scripts/BG0108d.baf~
END
